# Indexed Sets of Types

## Module

```agda
open import Level using (Level; suc; _âŠ”_)
open import Relation.Binary as RB using (
  Rel;
  Setoid;
  Antisym;
  IsEquivalence)
open import Relation.Binary.Indexed.Heterogeneous using (
  IRel;
  Reflexive;
  Symmetric;
  Transitive;
  IsIndexedEquivalence)
module Data.IndexedSet
  {c â„“ : Level}
  (S : Setoid c â„“)
  where
```

## Imports

```agda
open import Data.Empty.Polymorphic using (âŠ¥)
open import Data.Unit.Polymorphic using (âŠ¤; tt)

open import Data.Product using (_Ã—_; _,_; âˆƒ-syntax; Î£-syntax; projâ‚; projâ‚‚)
open import Relation.Binary.PropositionalEquality using (_â‰—_; subst)
open import Relation.Nullary using (Â¬_)

open import Function using (id; _âˆ˜_; Congruent; Surjective) --IsSurjection)

open Setoid S
private module Eq = IsEquivalence isEquivalence
```

## Definitions

```agda
Index : Set (suc c)
Index = Set c

IndexedSet : Index â†’ Set c
IndexedSet I = I â†’ Carrier

-- an element is within a subset, if there is an index pointing at that element
_âˆˆ_ : âˆ€ {I} â†’ Carrier â†’ IndexedSet I â†’ Set (c âŠ” â„“)
a âˆˆ A = âˆƒ[ i ] (a â‰ˆ A i)

-- morphisms
-- _âŠ†_ : âˆ€ {I J} â†’ IndexedSet I â†’ IndexedSet J â†’ Set â„“
_âŠ†_ : IRel IndexedSet (c âŠ” â„“)
_âŠ†_ {I} A B = âˆ€ (i : I) â†’ A i âˆˆ B

_â‰…_ : IRel IndexedSet (c âŠ” â„“)
A â‰… B = (A âŠ† B) Ã— (B âŠ† A)

-- Same indices point to same values.
-- This definition is the same as _â‰—_ from the standard-library but generalized to an arbitrary
-- underlying equivalence relation _â‰ˆ_.
_â‰_ : âˆ€ {I} (A B : IndexedSet I) â†’ Set (c âŠ” â„“)
_â‰_ {I} A B = âˆ€ (i : I) â†’ A i â‰ˆ B i

â‰â†’â‰… : âˆ€ {I} {A B : IndexedSet I} â†’ A â‰ B â†’ A â‰… B -- this acts as cong, too
â‰â†’â‰… Aâ‰B =
    (Î» i â†’ (i ,      Aâ‰B i))
  , (Î» i â†’ (i , sym (Aâ‰B i)))

â‰—â†’â‰ : âˆ€ {I} {A B : IndexedSet I} â†’ A â‰— B â†’ A â‰ B
â‰—â†’â‰ Aâ‰—B i = Eq.reflexive (Aâ‰—B i)

â‰—â†’â‰… : âˆ€ {I} {A B : IndexedSet I} â†’ A â‰— B â†’ A â‰… B
â‰—â†’â‰… Aâ‰—B = â‰â†’â‰… (â‰—â†’â‰ Aâ‰—B)
```

## Singletons

```agda
{-|
An indexed set contains only a single element if all indices point to the same element.
-}
Singleton : âˆ€ {I} â†’ IndexedSet I â†’ Set (c âŠ” â„“)
Singleton A = âˆƒ[ x ] âˆ€ i â†’ A i â‰ˆ x

irrelevant-index : âˆ€ {I} {A : IndexedSet I}
  â†’ Singleton A
  â†’ âˆ€ {i j} â†’ A i â‰ˆ A j
irrelevant-index (x , Aiâ‰ˆx) {i} {j} = Eq.trans (Aiâ‰ˆx i) (Eq.sym (Aiâ‰ˆx j))
```

## Properties

```agda
âŠ†-refl : Reflexive IndexedSet _âŠ†_
âŠ†-refl i = i , Eq.refl

-- Todo: There is no antsymmetry definition in Relation.Binary.Indexed.Heterogeneous.Definition. Adding that to the standard library would be good and a low hanging fruit.
âŠ†-antisym : âˆ€ {I J} â†’ Antisym (_âŠ†_ {I} {J}) (_âŠ†_ {J} {I}) (_â‰…_ {I} {J})
âŠ†-antisym l r = l , r

âŠ†-trans : Transitive IndexedSet _âŠ†_
âŠ†-trans AâŠ†B BâŠ†C i =
  -- This proof looks resembles state monad bind >>=.
  -- interesting... :thinking_face:
  let (j , Aiâ‰ˆBj) = AâŠ†B i
      (k , Bjâ‰ˆCk) = BâŠ†C j
   in k , Eq.trans Aiâ‰ˆBj Bjâ‰ˆCk

â‰…-refl : Reflexive IndexedSet _â‰…_
â‰…-refl = âŠ†-refl , âŠ†-refl

â‰…-sym : Symmetric IndexedSet _â‰…_
â‰…-sym (l , r) = r , l

â‰…-trans : Transitive IndexedSet _â‰…_
â‰…-trans (AâŠ†B , BâŠ†A) (BâŠ†C , CâŠ†B) =
    âŠ†-trans AâŠ†B BâŠ†C
  , âŠ†-trans CâŠ†B BâŠ†A

â‰…-IsIndexedEquivalence : IsIndexedEquivalence IndexedSet _â‰…_
â‰…-IsIndexedEquivalence = record
  { refl  = â‰…-refl
  ; sym   = â‰…-sym
  ; trans = â‰…-trans
  }

â‰-refl : âˆ€ {I} â†’ RB.Reflexive (_â‰_ {I})
â‰-refl i = refl

â‰-sym : âˆ€ {I} â†’ RB.Symmetric (_â‰_ {I})
â‰-sym xâ‰y i = sym (xâ‰y i)

â‰-trans : âˆ€ {I} â†’ RB.Transitive (_â‰_ {I})
â‰-trans xâ‰y yâ‰z i = trans (xâ‰y i) (yâ‰z i)

â‰-IsEquivalence : âˆ€ {I} â†’ IsEquivalence (_â‰_ {I})
â‰-IsEquivalence = record
  { refl = â‰-refl
  ; sym = â‰-sym
  ; trans = â‰-trans
  }
```

## Indexed Sets With Index Translations

```agda
_âŠ†[_]_ : âˆ€ {I J} â†’ IndexedSet I â†’ (I â†’ J) â†’ IndexedSet J â†’ Set (c âŠ” â„“)
_âŠ†[_]_ {I} A f B = âˆ€ (i : I) â†’ A i â‰ˆ B (f i)

_â‰…[_][_]_ : âˆ€ {I J} â†’ IndexedSet I â†’ (I â†’ J) â†’ (J â†’ I) â†’ IndexedSet J â†’ Set (c âŠ” â„“)
A â‰…[ f ][ fâ»Â¹ ] B = (A âŠ†[ f ] B) Ã— (B âŠ†[ fâ»Â¹ ] A)
```

### Relation to Ordinary Indexed Set Operators

```agda
âˆˆ[]â†’âˆˆ : âˆ€ {I} {A : IndexedSet I} {a : Carrier} {i : I}
  â†’ a â‰ˆ A i
    ----------
  â†’ a âˆˆ A
âˆˆ[]â†’âˆˆ {i = i} eq = i , eq

âŠ†[]â†’âŠ† : âˆ€ {I J} {A : IndexedSet I} {B : IndexedSet J} {f : I â†’ J}
  â†’ A âŠ†[ f ] B
    -----------
  â†’ A âŠ† B
âŠ†[]â†’âŠ† AâŠ†[f]B i = âˆˆ[]â†’âˆˆ (AâŠ†[f]B i)
-- âŠ†[]â†’âŠ† {f = f} AâŠ†[f]B = Î» i â†’ f i , AâŠ†[f]B i -- equivalent definition

-- verbose name
-- TODO: eta-reducing e here makes Agda have an internal error when importing âŠ†[]â†’âŠ†.
--       We should isolate that behaviour and report it as a bug.
syntax âŠ†[]â†’âŠ† e = âŠ†-by-index-translation e

â‰…[]â†’â‰… : âˆ€ {I J} {A : IndexedSet I} {B : IndexedSet J} {f   : I â†’ J} {fâ»Â¹ : J â†’ I}
  â†’ A â‰…[ f ][ fâ»Â¹ ] B
    -----------------
  â†’ A â‰… B
â‰…[]â†’â‰… (AâŠ†[f]B , BâŠ†[fâ»Â¹]A) = âŠ†[]â†’âŠ† AâŠ†[f]B , âŠ†[]â†’âŠ† BâŠ†[fâ»Â¹]A

-- verbose name
syntax â‰…[]â†’â‰… e = â‰…-by-index-translation e

â‰â†’â‰…[] : âˆ€ {I} {A B : IndexedSet I} â†’ A â‰ B â†’ A â‰…[ id ][ id ] B
â‰â†’â‰…[] {J} {A} {B} Aâ‰B =
    (Î» i â†’      Aâ‰B i )
  , (Î» i â†’ sym (Aâ‰B i))

â‰—â†’â‰…[] : âˆ€ {I} {A B : IndexedSet I} â†’ A â‰— B â†’ A â‰…[ id ][ id ] B
â‰—â†’â‰…[] = â‰â†’â‰…[] âˆ˜ â‰—â†’â‰

irrelevant-index-âŠ† : âˆ€ {I J} {A : IndexedSet I} {B : IndexedSet J}
  â†’ (x : Carrier)
  â†’ (âˆ€ i â†’ A i â‰ˆ x)
  â†’ (âˆ€ j â†’ B j â‰ˆ x)
    ------------------------
  â†’ âˆ€ f â†’ A âŠ†[ f ] B
irrelevant-index-âŠ† _ const-A const-B =
  Î» f i â†’
    Eq.trans (const-A i) (Eq.sym (const-B (f i)))

irrelevant-index-â‰… : âˆ€ {I J} {A : IndexedSet I} {B : IndexedSet J}
  â†’ (x : Carrier)
  â†’ (âˆ€ i â†’ A i â‰ˆ x)
  â†’ (âˆ€ j â†’ B j â‰ˆ x)
    ------------------------
  â†’ âˆ€ f g â†’ A â‰…[ f ][ g ] B
irrelevant-index-â‰… x const-A const-B =
  Î» f g â†’
      irrelevant-index-âŠ† x const-A const-B f
    , irrelevant-index-âŠ† x const-B const-A g
```

### Basic Properties

```agda
âŠ†[]-refl : âˆ€ {I} {A : IndexedSet I} â†’ A âŠ†[ id ] A
âŠ†[]-refl = Î» _ â†’ Eq.refl

âŠ†[]-antisym : âˆ€ {I J} {A : IndexedSet I} {B : IndexedSet J} {f   : I â†’ J} {fâ»Â¹ : J â†’ I}
  â†’ A âŠ†[ f   ] B
  â†’ B âŠ†[ fâ»Â¹ ] A
    -----------------
  â†’ A â‰…[ f ][ fâ»Â¹ ] B
âŠ†[]-antisym AâŠ†B BâŠ†A = AâŠ†B , BâŠ†A

âŠ†[]-trans :
  âˆ€ {I J K} {A : IndexedSet I} {B : IndexedSet J} {C : IndexedSet K}
    {f : I â†’ J} {g : J â†’ K}
  â†’ A âŠ†[ f ] B
  â†’ B âŠ†[ g ] C
    --------------
  â†’ A âŠ†[ g âˆ˜ f ] C
âŠ†[]-trans {f = f} AâŠ†B BâŠ†C i = Eq.trans (AâŠ†B i) (BâŠ†C (f i))

â‰…[]-refl : âˆ€ {I} {A : IndexedSet I} â†’ A â‰…[ id ][ id ] A
â‰…[]-refl = âŠ†[]-refl , âŠ†[]-refl

â‰…[]-sym : âˆ€ {I J} {A : IndexedSet I} {B : IndexedSet J} {f : I â†’ J} {fâ»Â¹ : J â†’ I}
  â†’ A â‰…[ f ][ fâ»Â¹ ] B
    -----------------
  â†’ B â‰…[ fâ»Â¹ ][ f ] A
â‰…[]-sym (AâŠ†B , BâŠ†A) = BâŠ†A , AâŠ†B

â‰…[]-trans :
  âˆ€ {I J K} {A : IndexedSet I} {B : IndexedSet J} {C : IndexedSet K}
    {f : I â†’ J} {fâ»Â¹ : J â†’ I} {g : J â†’ K} {gâ»Â¹ : K â†’ J}
  â†’ A â‰…[ f ][ fâ»Â¹ ] B
  â†’ B â‰…[ g ][ gâ»Â¹ ] C
    ---------------------------
  â†’ A â‰…[ g âˆ˜ f ][ fâ»Â¹ âˆ˜ gâ»Â¹ ] C
â‰…[]-trans {A = A} {C = C} (AâŠ†B , BâŠ†A) (BâŠ†C , CâŠ†B) =
  âŠ†[]-trans {C = C} AâŠ†B BâŠ†C ,
  âŠ†[]-trans {C = A} CâŠ†B BâŠ†A

âŠ†â†’â‰… : âˆ€ {I J} {A : IndexedSet I} {B : IndexedSet J} â†’ (f : I â†’ J) â†’ (fâ»Â¹ : J â†’ I) â†’ f âˆ˜ fâ»Â¹ â‰— id â†’ A âŠ†[ f ] B â†’ A â‰…[ f ][ fâ»Â¹ ] B
âŠ†â†’â‰… {A = A} {B = B} f fâ»Â¹ fâˆ˜fâ»Â¹ AâŠ†B = AâŠ†B , (Î» i â†’ Eq.sym (subst (Î» j â†’ A (fâ»Â¹ i) â‰ˆ B j) (fâˆ˜fâ»Â¹ i) (AâŠ†B (fâ»Â¹ i))))
```

## Equational Reasoning

```agda
module âŠ†-Reasoning where
  infix  3 _âŠ†-âˆ
  infixr 2 _âŠ†âŸ¨âŸ©_ step-âŠ†
  infix  1 âŠ†-begin_

  âŠ†-begin_ : âˆ€{I J} {A : IndexedSet I} {B : IndexedSet J} â†’ A âŠ† B â†’ A âŠ† B
  âŠ†-begin_ AâŠ†B = AâŠ†B

  _âŠ†âŸ¨âŸ©_ : âˆ€ {I J} (A : IndexedSet I) {B : IndexedSet J} â†’ A âŠ† B â†’ A âŠ† B
  _ âŠ†âŸ¨âŸ© AâŠ†B = AâŠ†B

  step-âŠ† : âˆ€ {I J K} (A : IndexedSet I) {B : IndexedSet J} {C : IndexedSet K}
    â†’ B âŠ† C
    â†’ A âŠ† B
    â†’ A âŠ† C
  step-âŠ† _ BâŠ†C AâŠ†B = âŠ†-trans AâŠ†B BâŠ†C

  _âŠ†-âˆ : âˆ€ {I} (A : IndexedSet I) â†’ A âŠ† A
  _âŠ†-âˆ _ = âŠ†-refl

  syntax step-âŠ† A BâŠ†C AâŠ†B = A âŠ†âŸ¨ AâŠ†B âŸ© BâŠ†C

module â‰…-Reasoning where
  infix  3 _â‰…-âˆ
  infixr 2 _â‰…âŸ¨âŸ©_ step-â‰…-âŸ¨ step-â‰…-âŸ© step-â‰-âŸ¨ step-â‰-âŸ© --step-â‰…-by-index-translation
  infix  1 â‰…-begin_

  â‰…-begin_ : âˆ€{I J} {A : IndexedSet I} {B : IndexedSet J} â†’ A â‰… B â†’ A â‰… B
  â‰…-begin_ Aâ‰…B = Aâ‰…B

  _â‰…âŸ¨âŸ©_ : âˆ€ {I J} (A : IndexedSet I) {B : IndexedSet J} â†’ A â‰… B â†’ A â‰… B
  _ â‰…âŸ¨âŸ© Aâ‰…B = Aâ‰…B

  step-â‰…-âŸ© : âˆ€ {I J K} (A : IndexedSet I) {B : IndexedSet J} {C : IndexedSet K}
    â†’ B â‰… C
    â†’ A â‰… B
    â†’ A â‰… C
  step-â‰…-âŸ© _ Bâ‰…C Aâ‰…B = â‰…-trans Aâ‰…B Bâ‰…C

  -- step-â‰…-by-index-translation : âˆ€ {I J K} (A : IndexedSet I) {B : IndexedSet J} {C : IndexedSet K}
  --   â†’ (iâ†’j : I â†’ J)
  --   â†’ (jâ†’i : J â†’ I)
  --   â†’ (âˆ€ (i : I) â†’ A i â‰ˆ B (iâ†’j i))
  --   â†’ (âˆ€ (j : J) â†’ B j â‰ˆ A (jâ†’i j))
  --   â†’ B â‰… C
  --   â†’ A â‰… C
  -- step-â‰…-by-index-translation _ iâ†’j jâ†’i ti tj Bâ‰…C = â‰…-trans (âŠ†-by-index-translation iâ†’j ti , âŠ†-by-index-translation jâ†’i tj) Bâ‰…C

  step-â‰…-âŸ¨ : âˆ€ {I J K} (A : IndexedSet I) {B : IndexedSet J} {C : IndexedSet K}
    â†’ B â‰… C
    â†’ B â‰… A
    â†’ A â‰… C
  step-â‰…-âŸ¨ A Bâ‰…C Bâ‰…A = step-â‰…-âŸ© A Bâ‰…C (â‰…-sym Bâ‰…A)

  step-â‰-âŸ© : âˆ€ {I J} (A {B} : IndexedSet I) {C : IndexedSet J}
    â†’ B â‰… C
    â†’ A â‰ B
    â†’ A â‰… C
  step-â‰-âŸ© _ Bâ‰…C Aâ‰B = â‰…-trans (â‰â†’â‰… Aâ‰B) Bâ‰…C

  step-â‰-âŸ¨ : âˆ€ {I J} (A {B} : IndexedSet I) {C : IndexedSet J}
    â†’ B â‰… C
    â†’ B â‰ A
    â†’ A â‰… C
  step-â‰-âŸ¨ A Bâ‰…C Bâ‰A = step-â‰-âŸ© A Bâ‰…C (â‰-sym Bâ‰A)

  _â‰…-âˆ : âˆ€ {I} (A : IndexedSet I) â†’ A â‰… A
  _â‰…-âˆ _ = â‰…-refl

  syntax step-â‰…-âŸ© A Bâ‰…C Aâ‰…B = A â‰…âŸ¨ Aâ‰…B âŸ© Bâ‰…C
  syntax step-â‰…-âŸ¨ A Bâ‰…C Bâ‰…A = A â‰…âŸ¨ Bâ‰…A âŸ¨ Bâ‰…C
  syntax step-â‰-âŸ© A Bâ‰…C (Î» i â†’ Aiâ‰ˆBi) = A â‰[ i ]âŸ¨ Aiâ‰ˆBi âŸ© Bâ‰…C
  syntax step-â‰-âŸ¨ A Bâ‰…C (Î» i â†’ Biâ‰ˆAi) = A â‰[ i ]âŸ¨ Biâ‰ˆAi âŸ¨ Bâ‰…C
  -- syntax step-â‰…-by-index-translation A iâ†’j jâ†’i ti tj Bâ‰…C = A â‰…[ iâ†’j ]âŸ¨ ti âŸ©[ jâ†’i ]âŸ¨ tj âŸ© Bâ‰…C

module â‰…[]-Reasoning where
  infix  3 _â‰…[]-âˆ
  infixr 2 _â‰…[]âŸ¨âŸ©_ step-â‰…[]-âŸ¨ step-â‰…[]-âŸ© step-â‰[]-âŸ¨ step-â‰[]-âŸ©
  infix  1 â‰…[]-begin_

  â‰…[]-begin_ : âˆ€{I J} {A : IndexedSet I} {B : IndexedSet J} {f : I â†’ J} {g : J â†’ I}
    â†’ A â‰…[ f ][ g ] B
    â†’ A â‰…[ f ][ g ] B
  â‰…[]-begin_ Aâ‰…B = Aâ‰…B

  _â‰…[]âŸ¨âŸ©_ : âˆ€ {I J} {f : I â†’ J} {g : J â†’ I} (A : IndexedSet I) {B : IndexedSet J}
    â†’ A â‰…[ f ][ g ] B
    â†’ A â‰…[ f ][ g ] B
  _ â‰…[]âŸ¨âŸ© Aâ‰…B = Aâ‰…B

  step-â‰…[]-âŸ© : âˆ€ {I J K} (A : IndexedSet I) {B : IndexedSet J} {C : IndexedSet K}
               {f : I â†’ J} {fâ»Â¹ : J â†’ I} {g : J â†’ K} {gâ»Â¹ : K â†’ J}
    â†’ B â‰…[ g ][ gâ»Â¹ ] C
    â†’ A â‰…[ f ][ fâ»Â¹ ] B
    â†’ A â‰…[ g âˆ˜ f ][ fâ»Â¹ âˆ˜ gâ»Â¹ ] C
  step-â‰…[]-âŸ© _ Bâ‰…C Aâ‰…B = â‰…[]-trans Aâ‰…B Bâ‰…C

  step-â‰…[]-âŸ¨ : âˆ€ {I J K} (A : IndexedSet I) {B : IndexedSet J} {C : IndexedSet K}
                {f : I â†’ J} {fâ»Â¹ : J â†’ I} {g : J â†’ K} {gâ»Â¹ : K â†’ J}
    â†’ B â‰…[ g ][ gâ»Â¹ ] C
    â†’ B â‰…[ fâ»Â¹ ][ f ] A
    â†’ A â‰…[ g âˆ˜ f ][ fâ»Â¹ âˆ˜ gâ»Â¹ ] C
  step-â‰…[]-âŸ¨ A Bâ‰…C Bâ‰…A = step-â‰…[]-âŸ© A Bâ‰…C (â‰…[]-sym Bâ‰…A)

  step-â‰[]-âŸ© : âˆ€ {I J} (A {B} : IndexedSet I) {C : IndexedSet J}
              {g : I â†’ J} {ginv : J â†’ I}
    â†’ B â‰…[ g ][ ginv ] C
    â†’ A â‰ B
    â†’ A â‰…[ g ][ ginv ] C
  step-â‰[]-âŸ© _ Bâ‰…C Aâ‰B = â‰…[]-trans (â‰â†’â‰…[] Aâ‰B) Bâ‰…C

  step-â‰[]-âŸ¨ : âˆ€ {I J} (A {B} : IndexedSet I) {C : IndexedSet J}
                {g : I â†’ J} {ginv : J â†’ I}
    â†’ B â‰…[ g ][ ginv ] C
    â†’ B â‰ A
    â†’ A â‰…[ g ][ ginv ] C
  step-â‰[]-âŸ¨ A Bâ‰…C Bâ‰A = step-â‰[]-âŸ© A Bâ‰…C (â‰-sym Bâ‰A)

  _â‰…[]-âˆ : âˆ€ {I} (A : IndexedSet I)
    â†’ A â‰…[ id ][ id ] A
  _â‰…[]-âˆ _ = â‰…[]-refl

  syntax step-â‰…[]-âŸ© A Bâ‰…C Aâ‰…B = A â‰…[]âŸ¨ Aâ‰…B âŸ© Bâ‰…C
  syntax step-â‰…[]-âŸ¨ A Bâ‰…C Bâ‰…A = A â‰…[]âŸ¨ Bâ‰…A âŸ¨ Bâ‰…C
  syntax step-â‰[]-âŸ© A Bâ‰…C (Î» i â†’ Aiâ‰ˆBi) = A â‰[ i ]âŸ¨ Aiâ‰ˆBi âŸ© Bâ‰…C
  syntax step-â‰[]-âŸ¨ A Bâ‰…C (Î» i â†’ Biâ‰ˆAi) = A â‰[ i ]âŸ¨ Biâ‰ˆAi âŸ¨ Bâ‰…C
```

## Common sets and relations

```agda
{-|
The empty set
-}
ğŸ˜ : IndexedSet âŠ¥
ğŸ˜ = Î» ()

{-|
The type of singleton sets over a source.
-}
ğŸ™ : Set c
ğŸ™ = IndexedSet âŠ¤

-- predicate that checks whether a subset is nonempty
-- A set is non-empty when there exists at least one index.
nonempty : âˆ€ {I} â†’ IndexedSet I â†’ Set c
nonempty {I = I} _ = I --âˆƒ[ a ] (a âˆˆ A)

-- We can retrieve an element from a non-empty set.
-- This proves that our definition of nonempty indeed
-- implies that there is an element in each non-empty set.
get-from-nonempty : âˆ€ {I}
  â†’ (A : IndexedSet I)
  â†’ nonempty A
    ----------------
  â†’ Carrier
get-from-nonempty A i = A i

-- predicate that checks whether a subset is empty
empty : âˆ€ {I} â†’ IndexedSet I â†’ Set c
empty A = Â¬ (nonempty A)

ğŸ˜-is-empty : empty ğŸ˜
ğŸ˜-is-empty ()

ğŸ˜âŠ†A : âˆ€ {I} {A : IndexedSet I}
    -----
  â†’ ğŸ˜ âŠ† A
ğŸ˜âŠ†A = Î» ()

empty-setâŠ†ğŸ˜ : âˆ€ {I} {A : IndexedSet I}
  â†’ empty A
    -------
  â†’ A âŠ† ğŸ˜
empty-setâŠ†ğŸ˜ A-empty i with A-empty i
...| ()

all-empty-sets-are-equal : âˆ€ {I} {A : IndexedSet I}
  â†’ empty A
    -------
  â†’ A â‰… ğŸ˜
all-empty-sets-are-equal A-empty = empty-setâŠ†ğŸ˜ A-empty , ğŸ˜âŠ†A

singleton-set-is-nonempty : (A : ğŸ™) â†’ nonempty A
singleton-set-is-nonempty _ = tt
```

## Further Properties

### Reindexing

We can rename the indices of a multiset M to obtain a subset of M.
```agda

re-indexË¡ : âˆ€ {A B : Set c}
  â†’ (rename : A â†’ B)
  â†’ (M : IndexedSet B)
    ---------------------
  â†’ (M âˆ˜ rename) âŠ† M
re-indexË¡ rename _ a = rename a , Eq.refl
```

If the renaming renames every index (i.e., the renaming is surjective), the
renamed multiset is isomorphic to the original set M.
Surjectivity can be given over any two equality relations `_â‰ˆáµƒ_` (equality of renamed indices) and `_â‰ˆáµ‡_` (equality of original indices).
We do not require that both relations are indeed equivalence relations but only list those properties we actually need:
  - All indices are renamed. This means that the renaming has to be surjective with respect to
    - equivalence of renamed indices `_â‰ˆáµƒ_`
    - equivalence of original indices `_â‰ˆáµ‡_`
  - Equal original indices index equal source elements. This means that the equality of original indices `_â‰ˆáµ‡_` has to be congruent with
    respect to equivalence `_â‰ˆ_` of the source elements.
  - `Symmetric _â‰ˆáµƒ_`: reflexivity over renamed indices is required for a detail in the proof.
  - `Symmetric _â‰ˆáµ‡_`: symmetry over original indices is required for a detail in the proof.
```agda
re-indexÊ³ : âˆ€ {A B : Index}
    {_â‰ˆáµƒ_ : Rel A c} -- Equality of renamed indices
    {_â‰ˆáµ‡_ : Rel B c} -- Equality of original indices
  â†’ (rename : A â†’ B)
  â†’ (M : IndexedSet B)
  â†’ Surjective _â‰ˆáµƒ_ _â‰ˆáµ‡_ rename
  â†’ RB.Reflexive _â‰ˆáµƒ_
  â†’ RB.Symmetric _â‰ˆáµ‡_
  â†’ Congruent _â‰ˆáµ‡_ _â‰ˆ_ M
    ---------------------
  â†’ M âŠ† (M âˆ˜ rename)
re-indexÊ³ {A} {B} {_â‰ˆáµƒ_} {_â‰ˆáµ‡_} rename M rename-is-surjective â‰ˆáµƒ-refl â‰ˆáµ‡-sym M-is-congruent b =
  a , same-picks
  where suitable-a : Î£[ a âˆˆ A ] ({z : A} â†’ z â‰ˆáµƒ a â†’ rename z â‰ˆáµ‡ b)
        suitable-a = rename-is-surjective b

        a : A
        a = projâ‚ suitable-a

        same-picks : M b â‰ˆ M (rename a)
        same-picks = M-is-congruent (â‰ˆáµ‡-sym (projâ‚‚ suitable-a â‰ˆáµƒ-refl))

re-index : âˆ€ {A B : Index}
    {_â‰ˆáµƒ_ : Rel A c} -- Equality of renamed indices
    {_â‰ˆáµ‡_ : Rel B c} -- Equality of original indices
  â†’ (rename : A â†’ B)
  â†’ (M : IndexedSet B)
  â†’ Surjective _â‰ˆáµƒ_ _â‰ˆáµ‡_ rename
  â†’ RB.Reflexive _â‰ˆáµƒ_
  â†’ RB.Symmetric _â‰ˆáµ‡_
  â†’ Congruent _â‰ˆáµ‡_ _â‰ˆ_ M
    ---------------------------
  â†’ (M âˆ˜ rename) â‰… M
re-index {_â‰ˆáµƒ_ = _â‰ˆáµƒ_} rename M rename-is-surjective â‰ˆáµƒ-refl â‰ˆáµ‡-sym M-is-congruent =
    re-indexË¡ rename M
  , re-indexÊ³ {_â‰ˆáµƒ_ = _â‰ˆáµƒ_} rename M rename-is-surjective â‰ˆáµƒ-refl â‰ˆáµ‡-sym M-is-congruent
```

## Examples

```
-- module Examples where
--   open import Data.Nat using (â„•)
--   open import Data.Fin using (Fin; suc; zero)
--   open import Relation.Binary.PropositionalEquality as Peq
--   open Level using (0â„“)

--   ex12 : IndexedSet â„• Peq.isEquivalence (Fin 2)
--   ex12 zero = 1
--   ex12 (suc zero) = 2

--   ex21 : IndexedSet (Fin 2) â„•
--   ex21 zero = 2
--   ex21 (suc zero) = 1

--   12â‰…21 : ex12 â‰… ex21
--   projâ‚ 12â‰…21 zero = suc zero , Eq.refl
--   projâ‚ 12â‰…21 (suc zero) = zero , Eq.refl
--   projâ‚‚ 12â‰…21 zero = suc zero , Eq.refl
--   projâ‚‚ 12â‰…21 (suc zero) = zero , Eq.refl

--   -- When the source is smaller than the index, then we have a multi set.
--   exshrink : IndexedSet (Fin 2) âŠ¤
--   exshrink x = tt
```
