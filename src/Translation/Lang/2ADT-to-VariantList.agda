open import Framework.Definitions using (ğ”½; ğ•; ğ”¸; ğ”¼)
open import Data.Bool using (Bool; true; false; not; if_then_else_)
open import Relation.Binary using (DecidableEquality; Rel)
module Translation.Lang.2ADT-to-VariantList
  (F : ğ”½)
  (V : ğ•)
  (_==_ : DecidableEquality F)
  where

open import Data.List using (List; []; _âˆ·_; _âˆ·Ê³_)
open import Data.List.NonEmpty using (Listâº; _âˆ·_; _++âº_; _âº++âº_; toList; length)
open import Data.List.NonEmpty.Properties using (length-++âº)
open import Data.Nat using (â„•; zero; suc; _+_; _âˆ¸_; _<_; _â‰¤_; _â‰¤?_; _â‰¤áµ‡_; zâ‰¤n; sâ‰¤s; s<s) --_â‰¤?_)
open import Data.Nat.Properties using (â‰¤-trans; â‰°â‡’>; <â‡’â‰¤; mâ‰¤m+n)
open import Data.Product using (Î£; _,_; _Ã—_; projâ‚; projâ‚‚; âˆƒ-syntax)
open import Data.Empty using (âŠ¥; âŠ¥-elim)
open import Level using (0â„“)
open import Function using (id; _âˆ˜_; _$_)

open import Data.List.Relation.Unary.Any using (Any; here; there)
open import Data.List.Relation.Unary.All using (All; []; _âˆ·_) renaming (map to map-all)
open import Data.List.Relation.Unary.All.Properties using (Â¬Anyâ‡’AllÂ¬) renaming (++âº to All-++âº)
open import Data.List.Relation.Unary.AllPairs using (AllPairs; []; _âˆ·_; head)

open import Data.EqIndexedSet hiding (Index; _âˆˆ_)
open Data.EqIndexedSet.â‰…-Reasoning

open import Relation.Nullary.Negation using (Â¬_)
open import Relation.Nullary.Decidable using (does; proof; yes; no; False; True; isYes; isNo; fromWitness; toWitness; fromWitnessFalse; toWitnessFalse)
open import Relation.Binary using (Decidable; Symmetric)
open import Relation.Binary.PropositionalEquality as Eq using (_â‰¡_; refl; sym)
open Eq.â‰¡-Reasoning

open import Framework.VariabilityLanguage
open import Lang.2ADT F V
  using (2ADT; 2ADTL; leaf; _âŸ¨_,_âŸ©)
  renaming (âŸ¦_âŸ§ to âŸ¦_âŸ§â‚‚; Configuration to Confâ‚‚)
open import Lang.VariantList V
  using (VariantList; VariantListL)
  renaming (âŸ¦_âŸ§ to âŸ¦_âŸ§â‚—; Configuration to Confâ‚—)
open import Framework.Relation.Expressiveness V using (_â‰½_; expressiveness-by-translation)

-- imports for nicer holes
open import Util.List using (find-or-last; âº++âº-length; âº++âº-length-â‰¤; append-preserves; prepend-preserves-+; prepend-preserves-âˆ¸)
open import Util.AuxProofs using (<-cong-+Ë¡; if-cong)
open import Util.Suffix
open Data.List using (_++_; foldr)
open Data.List.NonEmpty using (head; tail)

-- A selection of a feature matches it to a boolean value.
record Selection : Set where
  constructor _â†£_
  field
    feature : F
    value : Bool

-- A list of selection which denotes a path from the root of a 2ADT to a leaf node.
Path : Set
Path = List Selection

-- Two Selections are considered different iff they have different features.
-- Notable, (A â†£ true) is not different to (A â†£ false).
different : Rel Selection 0â„“
different (A â†£ _) (B â†£ _) = False (A == B)

sym-different : Symmetric different
sym-different neq = fromWitnessFalse Î» eq â†’ toWitnessFalse neq (sym eq)

-- Two selections are considered the same of they assign the same feature.
same : Rel Selection 0â„“
same (A â†£ _) (B â†£ _) = True (A == B)

-- Checks whether a given features was assigned in a selection.
is : F â†’ Selection â†’ Set
is A (B â†£ _) = True (A == B)

is-refl : âˆ€ (D : F) â†’ (b : Bool) â†’ is D (D â†£ b)
is-refl _ _ = fromWitness refl

==-isYes-refl : âˆ€ (D : F) â†’ isYes (D == D) â‰¡ true
==-isYes-refl D with D == D
... | yes refl = refl
... | no Dâ‰ D = âŠ¥-elim (Dâ‰ D refl)

-- Checks whether a feature is assigned somewhere in a path.
_âˆˆ_ : F â†’ Path â†’ Set
a âˆˆ as = Any (is a) as

_âˆ‰_ : F â†’ Path â†’ Set
D âˆ‰ fs = Â¬ (D âˆˆ fs)

âˆ‰-head : âˆ€ {D x xs} â†’ D âˆ‰ (x âˆ· xs) â†’ (b : Bool) â†’ different x (D â†£ b)
âˆ‰-head Dâˆ‰xâˆ·xs b = fromWitnessFalse Î» xâ‰¡D â†’ Dâˆ‰xâˆ·xs (here (fromWitness (sym xâ‰¡D)))

âˆ‰-tail : âˆ€ {D x xs} â†’ D âˆ‰ (x âˆ· xs) â†’ D âˆ‰ xs
âˆ‰-tail Dâˆ‰xâˆ·xs Dâˆˆxs = Dâˆ‰xâˆ·xs (there Dâˆˆxs)

-- Finds the assigned value of a feature within a path.
getValue : âˆ€ {a fs} â†’ a âˆˆ fs â†’ Bool
getValue (here {_ â†£ value} _) = value
getValue (there fs) = getValue fs

-- Containment _âˆˆ_ is decidable.
_âˆˆ?_ : Decidable _âˆˆ_
a âˆˆ? [] = no Î» ()
a âˆˆ? ((x â†£ v) âˆ· xs) with a == x
... | yes p = yes (here (fromWitness p))
... | no Â¬p with a âˆˆ? xs
...   | yes q = yes (there q)
...   | no Â¬q = no nope
  where
    nope : Â¬ Any (is a) ((x â†£ v) âˆ· xs)
    nope (here  p) = Â¬p (toWitness p)
    nope (there q) = Â¬q q

-- Turns an Â¬ Any to a All Â¬.
-- TODO: Reuse Â¬Anyâ‡’AllÂ¬ from the standard library.
âˆ‰â†’All-different : âˆ€ {D} â†’ (as : Path) â†’ D âˆ‰ as â†’ All (different (D â†£ true)) as
âˆ‰â†’All-different [] _ = []
âˆ‰â†’All-different (a âˆ· as) nope =
    fromWitnessFalse (Î» x â†’ nope (here (fromWitness x)))
  âˆ· âˆ‰â†’All-different as Î» x â†’ nope (there x)

All-differentâ†’âˆ‰ : âˆ€ {D} (b : Bool) (as : Path) â†’ All (different (D â†£ b)) as â†’ D âˆ‰ as
All-differentâ†’âˆ‰ b (a âˆ· as) (pa âˆ· pas) (here D-is-a) = toWitnessFalse pa (toWitness D-is-a)
All-differentâ†’âˆ‰ b (a âˆ· as) (pa âˆ· pas) (there Dâˆˆas)  = All-differentâ†’âˆ‰ b as pas Dâˆˆas

{-
All features mentioned in the path are unique (i.e., no feature is mentioned more than once).
This means that there cannot be conflicts in the path.
-}
Unique : Path â†’ Set
Unique = AllPairs different

{-
A path is starts at a node if it leads to a leaf.
This relation can be seen as a type system for paths within a particular tree.
Note: The symmetry between the rules walk-left and walk-right causes many
      proofs to have quite some redundancy as well because we have to match
      on these rules a lot.
      However, we cannot merge the rules into a single rule
      because we have to recurse on either the left or right alternative (not both).
-}
data _starts-at_ : âˆ€ {A} â†’ (p : Path) â†’ (e : 2ADT A) â†’ Set where
  tleaf : âˆ€ {A} {v : V A}
      ------------------
    â†’ [] starts-at (leaf v)

  walk-left : âˆ€ {A} {D : F} {l r : 2ADT A} {pl : Path}
    â†’ pl starts-at l
      -------------------------------------
    â†’ ((D â†£ true) âˆ· pl) starts-at (D âŸ¨ l , r âŸ©)

  walk-right : âˆ€ {A} {D : F} {l r : 2ADT A} {pr : Path}
    â†’ pr starts-at r
      --------------------------------------
    â†’ ((D â†£ false) âˆ· pr) starts-at (D âŸ¨ l , r âŸ©)

{-
An expression does not contain a feature name
if all paths do not contain that feature name.
-}
_âˆ‰'_ : âˆ€{A} â†’ F â†’ 2ADT A â†’ Set
D âˆ‰' e = âˆ€ (p : Path) â†’ p starts-at e â†’ D âˆ‰ p

{-
A path serves as a configuration for an expression e
if it starts at that expression and ends at a leaf.
-}
record PathConfig {A} (e : 2ADT A) : Set where
  constructor _is-valid_
  field
    path : Path
    valid : path starts-at e
open PathConfig public

{-
Alternative semantics of 2ADTs by walking a path.
This walk may be illegal by choosing different alternatives for the same choice within a path.
For example in D âŸ¨ D âŸ¨ 1 , dead âŸ© , 2 âŸ© we can reach 'dead' via (D â†£ true âˆ· D â†£ false âˆ· []).
However, walking like this is fine as long as the path is unique as we will later prove.
-}
walk : âˆ€ {A} â†’ (e : 2ADT A) â†’ PathConfig e â†’ V A
walk (leaf v) ([] is-valid tleaf) = v
walk (D âŸ¨ l , _ âŸ©) ((.(D â†£ true ) âˆ· pl) is-valid walk-left  t) = walk l (pl is-valid t)
walk (D âŸ¨ _ , r âŸ©) ((.(D â†£ false) âˆ· pr) is-valid walk-right t) = walk r (pr is-valid t)

{-
An expression a is a sub-expression of b
iff all valid paths from a lead to paths from b.
-}
_subexprof_ : âˆ€ {A} â†’ 2ADT A â†’ 2ADT A â†’ Set
a subexprof b = âˆ€ (pa : Path) â†’ pa starts-at a â†’ âˆƒ[ pb ] ((pb starts-at b) Ã— (pb endswith pa))

{-
A configuration matches a selection
if the configuration returns the same
result for the given feature as dictated
by the selection.
-}
matches : Confâ‚‚ â†’ Selection â†’ Set
matches c (f â†£ val) = c f â‰¡ val

{-
Predicate that tells whether a path matches a configuration.
This essentially makes the given path a partial configuration.
-}
_âŠ‘_ : Path â†’ Confâ‚‚ â†’ Set
p âŠ‘ c = All (matches c) p

{-
If we make a lookup in a path for a certain feature,
any matching configuration will yield the same result.
-}
lookup-partial : âˆ€ {p} {c} {D}
  â†’ p âŠ‘ c
  â†’ (has : D âˆˆ p)
  â†’ getValue has â‰¡ c D
lookup-partial (refl âˆ· px) (here D-is-f) rewrite toWitness D-is-f = refl
lookup-partial (refl âˆ· px) (there has) = lookup-partial px has

{-
A 2ADT is undead if it does not contain any dead branches.
This is the case if any path from the root to a leaf does not contain
a feature name twice.
-}
Undead : âˆ€ {A} (e : 2ADT A) â†’ Set
Undead e = âˆ€ (p : Path) â†’ p starts-at e â†’ Unique p

{-
A leaf node is always undead.
-}
undead-leaf : âˆ€ {A} {v : V A}
    ---------------
  â†’ Undead (leaf v)
undead-leaf .[] tleaf = []

{-
If a choice is undead, so is its left alternative.
-}
undead-left : âˆ€ {A} {D} {l r : 2ADT A}
  â†’ Undead (D âŸ¨ l , r âŸ©)
    --------------------
  â†’ Undead l
undead-left {D = D} u-chc p t with u-chc (D â†£ true âˆ· p) (walk-left t)
... | _ âˆ· u-p = u-p

{-
If a choice is undead, so is its right alternative.
-}
undead-right : âˆ€ {A} {D} {l r : 2ADT A}
  â†’ Undead (D âŸ¨ l , r âŸ©)
    --------------------
  â†’ Undead r
undead-right {D = D} u-chc p t with u-chc (D â†£ false âˆ· p) (walk-right t)
... | _ âˆ· u-p = u-p

{-
If two expressions l and r are undead and do
not contain the feature name D,
then the choice D âŸ¨ l , r âŸ© is undead, too.
-}
undead-choice : âˆ€ {A} {D} {l r : 2ADT A}
  â†’ Undead l
  â†’ Undead r
    -- It might be handy to introduce a new predicate for containment of feature names in expressions D âˆˆ l later.
  â†’ D âˆ‰' l
  â†’ D âˆ‰' r
    --------------------------------------
  â†’ Undead (D âŸ¨ l , r âŸ©)
undead-choice u-l u-r Dâˆ‰l Dâˆ‰r (.(_ â†£ true ) âˆ· p) (walk-left  t) = âˆ‰â†’All-different p (Dâˆ‰l p t) âˆ· (u-l p t)
undead-choice u-l u-r Dâˆ‰l Dâˆ‰r (.(_ â†£ false) âˆ· p) (walk-right t) = âˆ‰â†’All-different p (Dâˆ‰r p t) âˆ· (u-r p t)

record Undead2ADT (A : ğ”¸) : Set where
  constructor _âŠš_ -- \oo
  field
    node   : 2ADT A
    undead : Undead node
open Undead2ADT public

âŸ¦_âŸ§áµ¤ : ğ”¼-Semantics V Confâ‚‚ Undead2ADT
âŸ¦_âŸ§áµ¤ = âŸ¦_âŸ§â‚‚ âˆ˜ node

Undead2ADT-VL : VariabilityLanguage V
Undead2ADT-VL = âŸª Undead2ADT , Confâ‚‚ , âŸ¦_âŸ§áµ¤ âŸ«

{-
Kills all dead branches within a given expression,
assuming that some features were already defined.
-}
kill-dead-below : âˆ€ {A}
  â†’ (defined : Path)
  â†’ 2ADT A
  â†’ 2ADT A
kill-dead-below _ (leaf v) = leaf v
kill-dead-below defined (D âŸ¨ l , r âŸ©) with D âˆˆ? defined
--- The current choice was already encountered above this choice.
--- This means, this choice is dominated (see choice domination).
--- This in turn means, that one of the choice's alternatives is dead because it cannot
--- be selected anymore.
... | yes Dâˆˆdefined =
  if getValue Dâˆˆdefined
  then kill-dead-below defined l
  else kill-dead-below defined r
-- The current choice was not encountered before. We follow both paths recursively,
-- adding our choice information to each path.
... | no Dâˆ‰defined = D âŸ¨ kill-dead-below (D â†£ true âˆ· defined) l , kill-dead-below (D â†£ false âˆ· defined) r âŸ©

{-
If a feature name was already defined,
then any path starting at an expression,
in which dead branches were eliminated accordingly,
does not contain that feature name.
-}
kill-dead-eliminates-defined-features : âˆ€ {A}
  â†’ (defined : Path)
  â†’ (D : F)
  â†’ D âˆˆ defined
  â†’ (e : 2ADT A)
  â†’ D âˆ‰' kill-dead-below defined e
kill-dead-eliminates-defined-features _ _ _ (leaf _) .[] tleaf ()
kill-dead-eliminates-defined-features defined _ _ (D' âŸ¨ _ , _ âŸ©) _ _ _ with D' âˆˆ? defined
kill-dead-eliminates-defined-features defined D D-def (D' âŸ¨ l , r âŸ©) p t Dâˆˆp | yes D'-def with getValue D'-def
... | true  = kill-dead-eliminates-defined-features defined D D-def l p t Dâˆˆp
... | false = kill-dead-eliminates-defined-features defined D D-def r p t Dâˆˆp
kill-dead-eliminates-defined-features _ D _ (D' âŸ¨ _ , _ âŸ©) _ _ _
  | no Â¬D'-def with D == D'
kill-dead-eliminates-defined-features _ _ D-def (_  âŸ¨ _ , _ âŸ©) _ _ _
  | no Â¬D'-def | yes refl = âŠ¥-elim (Â¬D'-def D-def)
kill-dead-eliminates-defined-features _ _       _     (D' âŸ¨ _ , _ âŸ©) ((.D' â†£ true) âˆ· _) (walk-left _) (here D=D')
  | no _       | no Dâ‰ D'  = âŠ¥-elim (Dâ‰ D' (toWitness D=D'))
kill-dead-eliminates-defined-features defined D D-def (D' âŸ¨ l , _ âŸ©) ((.D' â†£ true) âˆ· p) (walk-left t) (there Dâˆˆp)
  | no Â¬D'-def | no Dâ‰ D'  = kill-dead-eliminates-defined-features (D' â†£ true âˆ· defined) D (there D-def) l p t Dâˆˆp
kill-dead-eliminates-defined-features _ _       _     (D' âŸ¨ _ , _ âŸ©) ((.D' â†£ false) âˆ· _) (walk-right _) (here D=D')
  | no _       | no Dâ‰ D'  = âŠ¥-elim (Dâ‰ D' (toWitness D=D'))
kill-dead-eliminates-defined-features defined D D-def (D' âŸ¨ _ , r âŸ©) ((.D' â†£ false) âˆ· p) (walk-right t) (there Dâˆˆp)
  | no Â¬D'-def | no Dâ‰ D'  = kill-dead-eliminates-defined-features (D' â†£ false âˆ· defined) D (there D-def) r p t Dâˆˆp

{-
Proof that kill-dead is correct,
which means that the resulting tree
is undead.
-}
kill-dead-correct : âˆ€ {A}
  â†’ (defined : Path)
  â†’ (e : 2ADT A)
  â†’ Undead (kill-dead-below defined e)
kill-dead-correct _ (leaf v) = undead-leaf
kill-dead-correct defined (D âŸ¨ _ , _ âŸ©) with D âˆˆ? defined
kill-dead-correct defined (_ âŸ¨ l , r âŸ©) | yes Dâˆˆdefined with getValue Dâˆˆdefined
... | true  = kill-dead-correct defined l
... | false = kill-dead-correct defined r
kill-dead-correct defined (D âŸ¨ l , r âŸ©) | no  Dâˆ‰defined =
  undead-choice
  (kill-dead-correct (D â†£ true  âˆ· defined) l)
  (kill-dead-correct (D â†£ false âˆ· defined) r)
  (kill-dead-eliminates-defined-features (D â†£ true  âˆ· defined) D (here (is-refl D true )) l)
  (kill-dead-eliminates-defined-features (D â†£ false âˆ· defined) D (here (is-refl D false)) r)

{-
Dead branch elimination of 2ADTs.
-}
kill-dead : âˆ€ {A}
  â†’ 2ADT A
  â†’ Undead2ADT A
kill-dead e = kill-dead-below [] e âŠš kill-dead-correct [] e

{-
This translates a 2ADT to a VariantList.
This is correct only if the 2ADT is undead.
Otherwise, also dead variants will be part of
the resulting list.
-}
tr : âˆ€ {A : ğ”¸} â†’ 2ADT A â†’ VariantList A
tr (leaf v) = v âˆ· []
tr (D âŸ¨ l , r âŸ©) = tr l âº++âº tr r

toVariantList : âˆ€ {A : ğ”¸} â†’ 2ADT A â†’ VariantList A
toVariantList = tr âˆ˜ node âˆ˜ kill-dead

{-
This module proves that dead branch elimination preserves semantics.
-}
module DeadBranchElim where
  kill-dead-preserves-below-partial-configs : âˆ€ {A : ğ”¸}
    â†’ (e : 2ADT A)
    â†’ (defined : Path)
    â†’ (c : Confâ‚‚)
    â†’ defined âŠ‘ c
    â†’ âŸ¦ e âŸ§â‚‚ c â‰¡ âŸ¦ kill-dead-below defined e âŸ§â‚‚ c
  kill-dead-preserves-below-partial-configs (leaf _) _ _ _ = refl
  kill-dead-preserves-below-partial-configs (D âŸ¨ l , r âŸ©) def c defâŠ‘c with D âˆˆ? def
  kill-dead-preserves-below-partial-configs (D âŸ¨ l , r âŸ©) def c defâŠ‘c | yes Dâˆˆdef
    rewrite lookup-partial defâŠ‘c Dâˆˆdef
    with c D
  ... | true  = kill-dead-preserves-below-partial-configs l def c defâŠ‘c
  ... | false = kill-dead-preserves-below-partial-configs r def c defâŠ‘c
  kill-dead-preserves-below-partial-configs (D âŸ¨ l , r âŸ©) def c defâŠ‘c | no Dâˆ‰def
    with c D in eq
  ... | true  = kill-dead-preserves-below-partial-configs l ((D â†£  true) âˆ· def) c (eq âˆ· defâŠ‘c)
  ... | false = kill-dead-preserves-below-partial-configs r ((D â†£ false) âˆ· def) c (eq âˆ· defâŠ‘c)

  -- Killing dead branches is ok.
  kill-dead-preserves : âˆ€ {A : ğ”¸}
    â†’ (e : 2ADT A)
    â†’ âŸ¦ e âŸ§â‚‚ â‰… âŸ¦ kill-dead e âŸ§áµ¤
  kill-dead-preserves e = â‰â†’â‰… (Î» c â†’ kill-dead-preserves-below-partial-configs e [] c [])

{-
This module proves that 'tr' preserves walk-semantics.
This means that when we evaluate 2ADTs by just walking "randomly"
down them, then simply converting a 2ADT to a variant list by
gathering all variants in leafs from left to right preserves semantics.
-}
module WalkToList where
  -- Converts a path to in the input 2ADT to the index in the resulting list.
  conff : âˆ€ {A} â†’ (e : 2ADT A) â†’ PathConfig e â†’ â„•
  conff .(leaf _) (.[] is-valid tleaf) = 0
  conff (D âŸ¨ l , _ âŸ©) ((_ âˆ· pl) is-valid walk-left  t) = conff l (pl is-valid t)
  conff (D âŸ¨ l , r âŸ©) ((_ âˆ· pr) is-valid walk-right t) = length (tr l) + conff r (pr is-valid t)

  -- Converts an index from the resulting list back to a path in the input 2ADT.
  ffnoc : âˆ€ {A} â†’ (e : 2ADT A) â†’ â„• â†’ PathConfig e
  ffnoc (leaf v) _ = [] is-valid tleaf
  ffnoc (D âŸ¨ l , r âŸ©) i with length (tr l) â‰¤? i
  ffnoc (D âŸ¨ l , r âŸ©) i | no _ {-left-} with ffnoc l i
  ... | pl is-valid tl = ((D â†£ true) âˆ· pl) is-valid walk-left tl
  ffnoc (D âŸ¨ l , r âŸ©) i | yes _  {-right-} with ffnoc r (i âˆ¸ (length (tr l)))
  ... | pr is-valid tr = ((D â†£ false) âˆ· pr) is-valid walk-right tr

  -- The index of a path will never be out of bounds.
  conff-bounded : âˆ€ {A}
    â†’ (e : 2ADT A)
    â†’ (c : PathConfig e)
    â†’ conff e c < length (tr e)
  conff-bounded (leaf v) (.[] is-valid tleaf) = sâ‰¤s zâ‰¤n
  conff-bounded (D âŸ¨ l , r âŸ©) ((.D â†£ true  âˆ· p) is-valid walk-left  t) = â‰¤-trans (conff-bounded l (p is-valid t)) (âº++âº-length-â‰¤ (tr l) (tr r))
  conff-bounded (D âŸ¨ l , r âŸ©) ((.D â†£ false âˆ· p) is-valid walk-right t) = go
    where
      c = p is-valid t

      -- get this by induction
      rb : conff r c < length (tr r)
      rb = conff-bounded r c

      -- add (length (tr l)) to both sides on the left
      gox : length (tr l) + conff r c < length (tr l) + length (tr r)
      gox = <-cong-+Ë¡ (length (tr l)) rb

      -- use the sum rule for âº++âº on the right hand side
      go : length (tr l) + conff r c < length (tr l âº++âº tr r)
      go rewrite âº++âº-length (tr l) (tr r) = gox

  preservation-walk-to-list-conf : âˆ€ {A : ğ”¸}
    â†’ (e : 2ADT A)
    â†’ walk e âŠ†[ conff e ] âŸ¦ tr e âŸ§â‚—
  preservation-walk-to-list-conf .(leaf _) (.[] is-valid tleaf) = refl
  preservation-walk-to-list-conf (D âŸ¨ l , r âŸ©) ((_ âˆ· pl) is-valid walk-left t) =
    let c = pl is-valid t
    in
    begin
      walk l c
    â‰¡âŸ¨ preservation-walk-to-list-conf l c âŸ©
      âŸ¦ tr l âŸ§â‚— (conff l c)
    â‰¡Ë˜âŸ¨ append-preserves (tr l) (tr r) (conff-bounded l c) âŸ©
      âŸ¦ tr l âº++âº tr r âŸ§â‚— (conff l c)
    âˆ
  preservation-walk-to-list-conf (D âŸ¨ l , r âŸ©) ((_ âˆ· pr) is-valid walk-right t) =
    let c = pr is-valid t
    in
    begin
      walk r c
    â‰¡âŸ¨ preservation-walk-to-list-conf r c âŸ©
      âŸ¦ tr r âŸ§â‚— (conff r c)
    â‰¡Ë˜âŸ¨ prepend-preserves-+ (conff r c) (tr l) (tr r) âŸ©
      âŸ¦ tr l âº++âº tr r âŸ§â‚— (length (tr l) + (conff r c))
    âˆ

  preservation-walk-to-list-fnoc : âˆ€ {A : ğ”¸}
    â†’ (e : 2ADT A)
    â†’ âŸ¦ tr e âŸ§â‚— âŠ†[ ffnoc e ] walk e
  preservation-walk-to-list-fnoc (leaf v) i = refl
  preservation-walk-to-list-fnoc (D âŸ¨ l , r âŸ©) i with length (tr l) â‰¤? i
  ... | no Â¬p =
    begin
      âŸ¦ tr (D âŸ¨ l , r âŸ©) âŸ§â‚— i
    â‰¡âŸ¨âŸ©
      find-or-last i ((tr l) âº++âº (tr r))
    â‰¡âŸ¨ append-preserves (tr l) (tr r) (â‰°â‡’> Â¬p) âŸ© -- this is satisfied by eq
      find-or-last i (tr l)
    â‰¡âŸ¨âŸ©
      âŸ¦ tr l âŸ§â‚— i
    â‰¡âŸ¨ preservation-walk-to-list-fnoc l i âŸ©
      walk l (path (ffnoc l i) is-valid valid (ffnoc l i))
    âˆ
  ... | yes len[tr-l]â‰¤i  =
    begin
      âŸ¦ tr (D âŸ¨ l , r âŸ©) âŸ§â‚— i
    â‰¡âŸ¨âŸ©
      find-or-last i ((tr l) âº++âº (tr r))
    â‰¡âŸ¨ prepend-preserves-âˆ¸ (tr l) (tr r) len[tr-l]â‰¤i âŸ©
      find-or-last (i âˆ¸ length (tr l)) (tr r)
    â‰¡âŸ¨âŸ©
      âŸ¦ tr r âŸ§â‚— (i âˆ¸ length (tr l))
    â‰¡âŸ¨ preservation-walk-to-list-fnoc r (i âˆ¸ length (tr l)) âŸ©
      walk r (path (ffnoc r (i âˆ¸ length (tr l))) is-valid valid (ffnoc r (i âˆ¸ length (tr l))))
    âˆ

  preservation-walk-to-list : âˆ€ {A : ğ”¸}
    â†’ (e : 2ADT A)
    â†’ walk e â‰… âŸ¦ tr e âŸ§â‚—
  preservation-walk-to-list e = â‰…[]â†’â‰… (preservation-walk-to-list-conf e , preservation-walk-to-list-fnoc e)

{-
Walk semantics are equivalent to ordinary 2ADT semantics
as long as the 2ADT does not contain any dead branches.
This means that configurations can be modelled as functions or as paths.
Interestingly, we obtain a compiler that does not change the input
expression but only translates configurations.
-}
module NoConflictWalk where
  {-
  Converts a configuration function to a valid path within
  the given tree.
  The conversion checks the configuration function at each choice,
  constructs the path accordingly, and recurses until it reaches a leaf.
  -}
  fun-to-path : âˆ€ {A} (e : 2ADT A) â†’ Confâ‚‚ â†’ PathConfig e
  fun-to-path (leaf _) _ = [] is-valid tleaf
  fun-to-path (D âŸ¨ _ , _ âŸ©) c with c D
  fun-to-path (D âŸ¨ l , _ âŸ©) c | true  with fun-to-path l c
  ... | pl is-valid tl = ((D â†£ true)  âˆ· pl) is-valid walk-left tl
  fun-to-path (D âŸ¨ _ , r âŸ©) c | false with fun-to-path r c
  ... | pr is-valid tr = ((D â†£ false) âˆ· pr) is-valid walk-right tr

  {-
  Converts a path for the given tree to a function.
  When the returned function is queried for the value of a feature D',
  the function walks the path until it finds the respective feature in the
  tree and returns the value specified in the path.
  Otherwise, returns true.
  (The returned function returns true for all features that
  are not on a valid path.)
  -}
  path-to-fun : âˆ€ {A} (e : 2ADT A) â†’ PathConfig e â†’ Confâ‚‚
  path-to-fun .(leaf _) ([] is-valid tleaf) _ = true
  path-to-fun (.D âŸ¨ l , r âŸ©) (((D â†£ .true) âˆ· p) is-valid walk-left t) D' =
    if (isYes (D == D'))
    then true
    else path-to-fun l (p is-valid t) D'
  path-to-fun (.D âŸ¨ l , r âŸ©) (((D â†£ .false) âˆ· p) is-valid walk-right t) D' =
    if (isYes (D == D'))
    then false
    else path-to-fun r (p is-valid t) D'

  {-
  When a given feature is not contained within a path,
  then the path cannot end in a sub-path containing that feature.
  TODO: There is probably a nicer, more general lemma hidden here.
  -}
  lem-not-endswith' : âˆ€ {D} {others q : Path}
    â†’ (b : Bool)
    â†’ All (different (D â†£ b)) others
    â†’ Â¬ (others endswith (D â†£ b âˆ· q))
  lem-not-endswith' b (px âˆ· all) (match .((_ â†£ b) âˆ· _)) = toWitnessFalse px refl
  lem-not-endswith' b (px âˆ· all) (later ends) = lem-not-endswith' b all ends

  -- more complex version of lem-not-endswith'
  lem-not-endswith : âˆ€ {D} {others q : Path}
    â†’ (b : Bool)
    â†’ All (different (D â†£ b)) others
    â†’ Â¬ (((D â†£ b) âˆ· others) endswith ((D â†£ not b) âˆ· q))
  lem-not-endswith false all (later ends) = lem-not-endswith' true all ends
  lem-not-endswith true all (later ends) = lem-not-endswith' false all ends

  {-
  Crucial lemma for proving preservation.
  path-to-fun returns the value b for a given feature D
  if the path given to path-to-fun contains the selection D â†£ b somewhere.
  -}
  path-to-fun-lem : âˆ€ {A} (D : F) (e : 2ADT A) (p q : Path) (t : p starts-at e)
    â†’ (b : Bool)
    â†’ Unique p
    â†’ p endswith ((D â†£ b) âˆ· q)
    â†’ path-to-fun e (p is-valid t) D â‰¡ b
  path-to-fun-lem D (D' âŸ¨ _ , _ âŸ©) (.D' â†£ true âˆ· _) _ (walk-left  _) _ _ _ with D' == D
  path-to-fun-lem _ _ _ _ _ false (u âˆ· _) x | yes refl = âŠ¥-elim (lem-not-endswith true u x)
  path-to-fun-lem _ _ _ _ _ true  _ _ | yes refl = refl
  path-to-fun-lem D (_ âŸ¨ l , _ âŸ©) (_ âˆ· others) q (walk-left  t) b (_ âˆ· u-o) x | no  Dâ‰ D' = path-to-fun-lem D l others q t b u-o (endswith-tail (Î» where refl â†’ Dâ‰ D' refl) x)
  path-to-fun-lem D (D' âŸ¨ _ , _ âŸ©) (D' â†£ false âˆ· _) q (walk-right t) b _ x with D' == D
  path-to-fun-lem _ _ _ _ _ false _ _ | yes refl = refl
  path-to-fun-lem _ _ _ _ _ true  (u âˆ· _) x | yes refl = âŠ¥-elim (lem-not-endswith false u x)
  path-to-fun-lem D (_ âŸ¨ _ , r âŸ©) (_ âˆ· others) q (walk-right  t) b (_ âˆ· u-o) x | no  Dâ‰ D' = path-to-fun-lem D r others q t b u-o (endswith-tail (Î» where refl â†’ Dâ‰ D' refl) x)

  {-
  If a path p ends in a sub-path with a certain selection,
  that selection is within p, too.
  -}
  endswith-path-contains : âˆ€ {p q : Path} {D}
    â†’ (b : Bool)
    â†’ p endswith ((D â†£ b) âˆ· q)
    â†’ D âˆˆ p
  endswith-path-contains _ (match .((_ â†£ _) âˆ· _)) = here (fromWitness refl)
  endswith-path-contains b (later x) = there (endswith-path-contains b x)

  path-to-fun-step-l-inner2 : âˆ€ {A}
    â†’ (D : F) (l r : 2ADT A)
    â†’ (p : Path) â†’ (t : p starts-at l)
    â†’ All (different (D â†£ true)) p
      -------------------------------------------------------------------
    â†’ (E : F) â†’ (E âˆˆ p)
    â†’   path-to-fun (D âŸ¨ l , r âŸ©) ((D â†£ true âˆ· p) is-valid walk-left t) E
      â‰¡ path-to-fun l (p is-valid t) E
  path-to-fun-step-l-inner2 D l r p t all-dims-in-p-different-to-D E Eâˆˆp with D == E
  ... | yes refl = âŠ¥-elim (All-differentâ†’âˆ‰ true p all-dims-in-p-different-to-D Eâˆˆp)
  ... | no _ = refl

  -- clone-and-own from path-to-fun-step-l-inner2
  path-to-fun-step-r-inner2 : âˆ€ {A}
    â†’ (D : F) (l r : 2ADT A)
    â†’ (p : Path) â†’ (t : p starts-at r)
    â†’ All (different (D â†£ false)) p
      -------------------------------------------------------------------
    â†’ (E : F) â†’ (E âˆˆ p)
    â†’   path-to-fun (D âŸ¨ l , r âŸ©) ((D â†£ false âˆ· p) is-valid walk-right t) E
      â‰¡ path-to-fun r (p is-valid t) E
  path-to-fun-step-r-inner2 D l r p t all-dims-in-p-different-to-D E Eâˆˆp with D == E
  ... | yes refl = âŠ¥-elim (All-differentâ†’âˆ‰ true p all-dims-in-p-different-to-D Eâˆˆp)
  ... | no _ = refl

  path-to-fun-step-l-inner : âˆ€ {A}
    -- for a choice D âŸ¨ l , r âŸ©
    â†’ (D : F) (l r : 2ADT A)
    â†’ (lp : Path)
    -- if there is a subexpression e
    â†’ (e : 2ADT A) (ep : Path)
    -- (i.e., all paths starting in l end in paths starting in e)
    â†’ (tlp : lp starts-at l)
    â†’ (tep : ep starts-at e)
    â†’ (sub : lp endswith ep)
    -- and if the left branch l is undead
    â†’ All (different (D â†£ true)) lp -- this also means All (different (D â†£ true)) ep
    â†’ Unique lp
    -- then it does not matter whether we convert the whole path from the choice to
    -- a function, or if we start at the left alternative below.
    â†’   âŸ¦ e âŸ§â‚‚ (path-to-fun (D âŸ¨ l , r âŸ©) ((D â†£ true âˆ· lp) is-valid walk-left tlp))
      â‰¡ âŸ¦ e âŸ§â‚‚ (path-to-fun l (lp is-valid tlp))
  path-to-fun-step-l-inner D l r lp (leaf v) ep tlp tep sub x _ = refl
  path-to-fun-step-l-inner D l r lp (D' âŸ¨ a , b âŸ©) ((.D' â†£ .true) âˆ· ep) tlp (walk-left tep) sub x u =
    begin
      âŸ¦ D' âŸ¨ a , b âŸ© âŸ§â‚‚ c-big
    â‰¡âŸ¨âŸ©
      (if c-big D' then âŸ¦ a âŸ§â‚‚ c-big else âŸ¦ b âŸ§â‚‚ c-big)
    â‰¡âŸ¨ Eq.cong (if_then âŸ¦ a âŸ§â‚‚ c-big else âŸ¦ b âŸ§â‚‚ c-big) (path-to-fun-step-l-inner2 D l r lp tlp x D' (endswith-Any sub (here (fromWitness refl)))) âŸ©
      (if c-sml D' then âŸ¦ a âŸ§â‚‚ c-big else âŸ¦ b âŸ§â‚‚ c-big)
    â‰¡âŸ¨ lem âŸ©
      (if c-sml D' then âŸ¦ a âŸ§â‚‚ c-sml else âŸ¦ b âŸ§â‚‚ c-sml)
    â‰¡âŸ¨âŸ©
      âŸ¦ D' âŸ¨ a , b âŸ© âŸ§â‚‚ c-sml
    âˆ
    where
      c-big = Î» D'' â†’ if isYes (D == D'') then true else path-to-fun l (lp is-valid tlp) D''
      c-sml = path-to-fun l (lp is-valid tlp)

      force : c-sml D' â‰¡ true
      force = path-to-fun-lem D' l lp ep tlp true u sub

      lem : (if c-sml D' then âŸ¦ a âŸ§â‚‚ c-big else âŸ¦ b âŸ§â‚‚ c-big) â‰¡ (if c-sml D' then âŸ¦ a âŸ§â‚‚ c-sml else âŸ¦ b âŸ§â‚‚ c-sml)
      lem rewrite force = path-to-fun-step-l-inner D l r lp a ep tlp tep (endswith-shrink-suffix sub) x u
  path-to-fun-step-l-inner D l r lp (D' âŸ¨ a , b âŸ©) ((.D' â†£ false) âˆ· ep) tlp (walk-right tep) sub x u
    -- These rewrites are copied from the case above.
    rewrite path-to-fun-step-l-inner2 D l r lp tlp x D' (endswith-Any sub (here (fromWitness refl)))
    rewrite path-to-fun-lem D' l lp ep tlp false u sub
    rewrite path-to-fun-step-l-inner D l r lp b ep tlp tep (endswith-shrink-suffix sub) x u
    = refl

  -- This is a huge copy and paste blob from
  -- path-to-fun-step-r-inner
  path-to-fun-step-r-inner : âˆ€ {A}
    â†’ (D : F) (l r : 2ADT A)
    â†’ (rp : Path)
    â†’ (e : 2ADT A) (ep : Path)
    â†’ (trp : rp starts-at r)
    â†’ (tep : ep starts-at e)
    â†’ (sub : rp endswith ep)
    â†’ All (different (D â†£ false)) rp
    â†’ Unique rp
    â†’   âŸ¦ e âŸ§â‚‚ (path-to-fun (D âŸ¨ l , r âŸ©) ((D â†£ false âˆ· rp) is-valid walk-right trp))
      â‰¡ âŸ¦ e âŸ§â‚‚ (path-to-fun r (rp is-valid trp))
  path-to-fun-step-r-inner D l r lp (leaf v) ep tlp tep sub x _ = refl
  path-to-fun-step-r-inner D l r lp (D' âŸ¨ a , b âŸ©) ((.D' â†£ .true) âˆ· ep) tlp (walk-left tep) sub x u
    rewrite path-to-fun-step-r-inner2 D l r lp tlp x D' (endswith-Any sub (here (fromWitness refl)))
    rewrite path-to-fun-lem D' r lp ep tlp true u sub
    rewrite path-to-fun-step-r-inner D l r lp a ep tlp tep (endswith-shrink-suffix sub) x u
    = refl
  path-to-fun-step-r-inner D l r lp (D' âŸ¨ a , b âŸ©) ((.D' â†£ false) âˆ· ep) tlp (walk-right tep) sub x u
    -- These rewrites are copied from the case above.
    rewrite path-to-fun-step-r-inner2 D l r lp tlp x D' (endswith-Any sub (here (fromWitness refl)))
    rewrite path-to-fun-lem D' r lp ep tlp false u sub
    rewrite path-to-fun-step-r-inner D l r lp b ep tlp tep (endswith-shrink-suffix sub) x u
    = refl

  path-to-fun-step-l : âˆ€ {A}
    â†’ (D : F) (l r : 2ADT A)
    â†’ Undead (D âŸ¨ l , r âŸ©)
    â†’ (p : Path)
    â†’ (t : p starts-at l)
    â†’   âŸ¦ l âŸ§â‚‚ (path-to-fun (D âŸ¨ l , r âŸ©) ((D â†£ true âˆ· p) is-valid walk-left t))
      â‰¡ âŸ¦ l âŸ§â‚‚ (path-to-fun l (p is-valid t))
  path-to-fun-step-l D l r u p t with u ((D â†£ true) âˆ· p) (walk-left t)
  ... | u âˆ· uu = path-to-fun-step-l-inner D l r p l p t t (match p) u uu

  path-to-fun-step-r : âˆ€ {A}
    â†’ (D : F) (l r : 2ADT A)
    â†’ Undead (D âŸ¨ l , r âŸ©)
    â†’ (p : Path)
    â†’ (t : p starts-at r)
    â†’   âŸ¦ r âŸ§â‚‚ (path-to-fun (D âŸ¨ l , r âŸ©) ((D â†£ false âˆ· p) is-valid walk-right t))
      â‰¡ âŸ¦ r âŸ§â‚‚ (path-to-fun r (p is-valid t))
  path-to-fun-step-r D l r u p t with u ((D â†£ false) âˆ· p) (walk-right t)
  ... | u âˆ· uu = path-to-fun-step-r-inner D l r p r p t t (match p) u uu

  path-to-fun-head : âˆ€ {A}
    â†’ (D : F)
    â†’ (l r : 2ADT A)
    â†’ (b : Bool)
    â†’ (p : Path)
    â†’ (t : ((D â†£ b) âˆ· p) starts-at (D âŸ¨ l , r âŸ©))
    â†’ path-to-fun (D âŸ¨ l , r âŸ©) (((D â†£ b) âˆ· p) is-valid t) D â‰¡ b
  path-to-fun-head D l r .true p (walk-left t) with D == D
  ... | yes Dâ‰¡D = refl
  ... | no  Dâ‰¢D = âŠ¥-elim (Dâ‰¢D refl)
  path-to-fun-head D l r .false p (walk-right t) with D == D
  ... | yes Dâ‰¡D = refl
  ... | no  Dâ‰¢D = âŠ¥-elim (Dâ‰¢D refl)

  preservation-path-configs-conf : âˆ€ {A : ğ”¸}
    â†’ (e : 2ADT A)
    â†’ (u : Undead e)
    â†’ âŸ¦ e âŠš u âŸ§áµ¤ âŠ†[ fun-to-path e ] walk e
  preservation-path-configs-conf (leaf _) _ _ = refl
  preservation-path-configs-conf (D âŸ¨ _ , _ âŸ©) _ c with c D
  preservation-path-configs-conf (_ âŸ¨ l , _ âŸ©) u c | true  with fun-to-path l c in eq
  ... | pl is-valid tl =
    begin
      âŸ¦ l âŸ§â‚‚ c
    â‰¡âŸ¨âŸ©
      âŸ¦ l âŠš undead-left u âŸ§áµ¤ c
    â‰¡âŸ¨ preservation-path-configs-conf l (undead-left u) c âŸ©
      walk l (fun-to-path l c)
    â‰¡âŸ¨ Eq.cong (walk l) eq âŸ©
      walk l (pl is-valid tl)
    âˆ
  preservation-path-configs-conf (_ âŸ¨ _ , r âŸ©) u c | false with fun-to-path r c in eq
  ... | _ rewrite (sym eq) = preservation-path-configs-conf r (undead-right u) c

  preservation-path-configs-fnoc : âˆ€ {A : ğ”¸}
    â†’ (e : 2ADT A)
    â†’ (u : Undead e)
    â†’ walk e âŠ†[ path-to-fun e ] âŸ¦ e âŠš u âŸ§áµ¤
  preservation-path-configs-fnoc (leaf v) _ (.[] is-valid tleaf) = refl
  preservation-path-configs-fnoc (D âŸ¨ l , r âŸ©) u c@((.(D â†£ true ) âˆ· p) is-valid walk-left t)
    rewrite path-to-fun-head D l r true p (walk-left t) =
    begin
      walk l (p is-valid t)
    â‰¡âŸ¨ preservation-path-configs-fnoc l (undead-left u) (p is-valid t) âŸ©
      âŸ¦ l âŸ§â‚‚ (path-to-fun l (p is-valid t))
    â‰¡Ë˜âŸ¨ path-to-fun-step-l D l r u p t âŸ©
      âŸ¦ l âŸ§â‚‚ (path-to-fun (D âŸ¨ l , r âŸ©) ((D â†£ true âˆ· p) is-valid walk-left t))
    â‰¡âŸ¨âŸ©
      âŸ¦ l âŸ§â‚‚ (Î» D' â†’ if isYes (D == D') then true else path-to-fun l (p is-valid t) D')
    âˆ
  preservation-path-configs-fnoc (D âŸ¨ l , r âŸ©) u ((.(D â†£ false) âˆ· p) is-valid walk-right t)
    rewrite path-to-fun-head D l r false p (walk-right t)
    rewrite preservation-path-configs-fnoc r (undead-right u) (p is-valid t)
    rewrite path-to-fun-step-r D l r u p t
    = refl

  preservation-path-configs : âˆ€ {A : ğ”¸}
    â†’ (e : Undead2ADT A)
    â†’ âŸ¦ e âŸ§áµ¤ â‰… walk (node e)
  preservation-path-configs e = â‰…[]â†’â‰… (preservation-path-configs-conf (node e) (undead e) , preservation-path-configs-fnoc (node e) (undead e))

-- 2ADTs are isomorphic to Variant Lists.
-- TODO: Construct compilers and make use of âŠ•.
preservation : âˆ€ {A : ğ”¸}
  â†’ (e : 2ADT A)
  â†’ âŸ¦ e âŸ§â‚‚ â‰… âŸ¦ toVariantList e âŸ§â‚—
preservation e =
  â‰…-begin
    âŸ¦ e âŸ§â‚‚
  â‰…âŸ¨ DeadBranchElim.kill-dead-preserves e âŸ© -- todo
    âŸ¦ kill-dead e âŸ§áµ¤
  â‰…âŸ¨ NoConflictWalk.preservation-path-configs (kill-dead e) âŸ© -- done
    walk (node (kill-dead e))
  â‰…âŸ¨ WalkToList.preservation-walk-to-list (node (kill-dead e)) âŸ© -- conceptually done
    âŸ¦ toVariantList e âŸ§â‚—
  â‰…-âˆ

VariantListâ‰½2ADT : VariantListL â‰½ 2ADTL
VariantListâ‰½2ADT = expressiveness-by-translation toVariantList preservation
