{-|
This module renames dimensions in algebraic decision trees.

The idea of this translation is to apply a renaming function `f : Dâ‚ â†’ Dâ‚‚` to
all elements of `Dâ‚` in the datastructure `ADT V Dâ‚` to obtain a new
datastructure `ADT V Dâ‚‚`. To prove preservation of the semantics, we also
require a left inverse `fâ»Â¹ : Dâ‚‚ â†’ Dâ‚` of `f` as a proof that `f` is injective.
This precondition is necessary because a non-injective rename would reduce the
number of possible variants.
-}

open import Framework.Definitions using (ğ”¸; ğ”½; ğ•)

module Translation.Lang.ADT.Rename (V : ğ•) where

open import Size using (Size; âˆ)
open import Data.Bool using (if_then_else_)
open import Data.Bool.Properties as Bool
import Data.EqIndexedSet as IndexedSet
open import Data.List as List using (List)
import Data.List.Properties as List
open import Data.Product using () renaming (_,_ to _and_)
open import Framework.Compiler using (LanguageCompiler)
open import Framework.Relation.Expressiveness V using (_â‰½_; expressiveness-from-compiler)
open import Framework.Relation.Function using (from; to)
open import Function using (id; _âˆ˜_)
open import Relation.Binary.PropositionalEquality as Eq using (refl; _â‰—_)

open Eq.â‰¡-Reasoning using (step-â‰¡-âŸ¨; step-â‰¡-âŸ©; step-â‰¡-âˆ£; _âˆ)
open IndexedSet using (_â‰…[_][_]_; _âŠ†[_]_; â‰…[]-sym)

open import Lang.All
open ADT using (ADT; ADTL; leaf; _âŸ¨_,_âŸ©)

ADT-map-config : âˆ€ {Dâ‚ Dâ‚‚ : ğ”½}
  â†’ (Dâ‚‚ â†’ Dâ‚)
  â†’ ADT.Configuration Dâ‚
  â†’ ADT.Configuration Dâ‚‚
ADT-map-config f config = config âˆ˜ f

rename : âˆ€ {Dâ‚ Dâ‚‚ : ğ”½} {A : ğ”¸}
  â†’ (Dâ‚ â†’ Dâ‚‚)
  â†’ ADT V Dâ‚ A
  â†’ ADT V Dâ‚‚ A
rename f (leaf v) = leaf v
rename f (d ADT.âŸ¨ l , r âŸ©) = f d âŸ¨ rename f l , rename f r âŸ©

preserves-âŠ† : âˆ€ {Dâ‚ Dâ‚‚ : ğ”½} {A : ğ”¸}
  â†’ (f : Dâ‚ â†’ Dâ‚‚)
  â†’ (expr : ADT V Dâ‚ A)
  â†’ ADT.âŸ¦ rename f expr âŸ§ âŠ†[ ADT-map-config f ] ADT.âŸ¦ expr âŸ§
preserves-âŠ† f (leaf v) config = refl
preserves-âŠ† f (d âŸ¨ l , r âŸ©) config =
    ADT.âŸ¦ rename f (d âŸ¨ l , r âŸ©) âŸ§ config
  â‰¡âŸ¨âŸ©
    ADT.âŸ¦ f d âŸ¨ rename f l , rename f r âŸ© âŸ§ config
  â‰¡âŸ¨âŸ©
    (if config (f d) then ADT.âŸ¦ rename f l âŸ§ config else ADT.âŸ¦ rename f r âŸ§ config)
  â‰¡âŸ¨ Eq.congâ‚‚ (if config (f d) then_else_) (preserves-âŠ† f l config) (preserves-âŠ† f r config) âŸ©
    (if config (f d) then ADT.âŸ¦ l âŸ§ (config âˆ˜ f) else ADT.âŸ¦ r âŸ§ (config âˆ˜ f))
  â‰¡âŸ¨âŸ©
    ADT.âŸ¦ d âŸ¨ l , r âŸ© âŸ§ (config âˆ˜ f)
  âˆ

preserves-âŠ‡ : âˆ€ {Dâ‚ Dâ‚‚ : ğ”½} {A : ğ”¸}
  â†’ (f : Dâ‚ â†’ Dâ‚‚)
  â†’ (fâ»Â¹ : Dâ‚‚ â†’ Dâ‚)
  â†’ fâ»Â¹ âˆ˜ f â‰— id
  â†’ (expr : ADT V Dâ‚ A)
  â†’ ADT.âŸ¦ expr âŸ§ âŠ†[ ADT-map-config fâ»Â¹ ] ADT.âŸ¦ rename f expr âŸ§
preserves-âŠ‡ f fâ»Â¹ is-inverse (leaf v) config = refl
preserves-âŠ‡ f fâ»Â¹ is-inverse (d âŸ¨ l , r âŸ©) config =
    ADT.âŸ¦ d âŸ¨ l , r âŸ© âŸ§ config
  â‰¡âŸ¨âŸ©
    (if config d then ADT.âŸ¦ l âŸ§ config else ADT.âŸ¦ r âŸ§ config)
  â‰¡âŸ¨ Eq.congâ‚‚ (if config d then_else_) (preserves-âŠ‡ f fâ»Â¹ is-inverse l config) (preserves-âŠ‡ f fâ»Â¹ is-inverse r config) âŸ©
    (if config d then ADT.âŸ¦ rename f l âŸ§ (config âˆ˜ fâ»Â¹) else ADT.âŸ¦ rename f r âŸ§ (config âˆ˜ fâ»Â¹))
  â‰¡âŸ¨ Eq.cong-app (Eq.cong-app (Eq.cong if_then_else_ (Eq.cong config (is-inverse d))) (ADT.âŸ¦ rename f l âŸ§ (config âˆ˜ fâ»Â¹))) (ADT.âŸ¦ rename f r âŸ§ (config âˆ˜ fâ»Â¹)) âŸ¨
    (if config (fâ»Â¹ (f d)) then ADT.âŸ¦ rename f l âŸ§ (config âˆ˜ fâ»Â¹) else ADT.âŸ¦ rename f r âŸ§ (config âˆ˜ fâ»Â¹))
  â‰¡âŸ¨âŸ©
    ADT.âŸ¦ f d âŸ¨ rename f l , rename f r âŸ© âŸ§ (config âˆ˜ fâ»Â¹)
  â‰¡âŸ¨âŸ©
    ADT.âŸ¦ rename f (d âŸ¨ l , r âŸ©) âŸ§ (config âˆ˜ fâ»Â¹)
  âˆ

preserves : âˆ€ {Dâ‚ Dâ‚‚ : ğ”½} {A : ğ”¸}
  â†’ (f : Dâ‚ â†’ Dâ‚‚)
  â†’ (fâ»Â¹ : Dâ‚‚ â†’ Dâ‚)
  â†’ fâ»Â¹ âˆ˜ f â‰— id
  â†’ (e : ADT V Dâ‚ A)
  â†’ ADT.âŸ¦ rename f e âŸ§ â‰…[ ADT-map-config f ][ ADT-map-config fâ»Â¹ ] ADT.âŸ¦ e âŸ§
preserves f fâ»Â¹ is-inverse expr = preserves-âŠ† f expr and preserves-âŠ‡ f fâ»Â¹ is-inverse expr

ADT-rename : âˆ€ {Dâ‚ Dâ‚‚ : ğ”½}
  â†’ (f : Dâ‚ â†’ Dâ‚‚)
  â†’ (fâ»Â¹ : Dâ‚‚ â†’ Dâ‚)
  â†’ fâ»Â¹ âˆ˜ f â‰— id
  â†’ LanguageCompiler (ADTL V Dâ‚) (ADTL V Dâ‚‚)
ADT-rename f fâ»Â¹ is-inverse .LanguageCompiler.compile = rename f
ADT-rename f fâ»Â¹ is-inverse .LanguageCompiler.config-compiler expr .to = ADT-map-config fâ»Â¹
ADT-rename f fâ»Â¹ is-inverse .LanguageCompiler.config-compiler expr .from = ADT-map-config f
ADT-rename f fâ»Â¹ is-inverse .LanguageCompiler.preserves expr = â‰…[]-sym (preserves f fâ»Â¹ is-inverse expr)

ADT-renameâ‰½ADT : âˆ€ {Dâ‚ Dâ‚‚ : Set}
  â†’ (f : Dâ‚ â†’ Dâ‚‚)
  â†’ (fâ»Â¹ : Dâ‚‚ â†’ Dâ‚)
  â†’ fâ»Â¹ âˆ˜ f â‰— id
  â†’ ADTL V Dâ‚‚ â‰½ ADTL V Dâ‚
ADT-renameâ‰½ADT f fâ»Â¹ is-inverse = expressiveness-from-compiler (ADT-rename f fâ»Â¹ is-inverse)
