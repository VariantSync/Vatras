{-# OPTIONS --sized-types #-}

open import Framework.Construct using (_âˆˆâ‚›_; cons)
open import Framework.Definitions using (ğ”¸; ğ”½; ğ•; atoms)
open import Construct.Artifact as At using () renaming (Syntax to Artifact; _-<_>- to artifact-constructor)

module Translation.Lang.2CC-to-2ADT (Variant : ğ•) (Artifactâˆˆâ‚›Variant : Artifact âˆˆâ‚› Variant) where

import Data.EqIndexedSet as IndexedSet
open import Data.Bool as Bool using (if_then_else_)
import Data.Bool.Properties as Bool
open import Data.List as List using (List; []; _âˆ·_; _Ê³++_)
import Data.List.Properties as List
open import Framework.Compiler using (LanguageCompiler)
open import Framework.Relation.Expressiveness Variant using (expressiveness-from-compiler; _â‰½_)
open import Framework.Relation.Function using (from; to)
open import Function using (id)
open import Relation.Binary.PropositionalEquality as Eq using (_â‰¡_; refl; _â‰—_)
open import Size using (Size)

open Eq.â‰¡-Reasoning using (step-â‰¡; step-â‰¡Ë˜; _â‰¡âŸ¨âŸ©_; _âˆ)
open IndexedSet using (_â‰…[_][_]_; â‰…[]-sym; â‰—â†’â‰…[])

open import Lang.All.Generic Variant Artifactâˆˆâ‚›Variant
open 2CC using (2CC; 2CCL)
open 2ADT using (2ADT; 2ADTL; leaf; _âŸ¨_,_âŸ©)

artifact : âˆ€ {A : ğ”¸} â†’ atoms A â†’ List (Variant A) â†’ Variant A
artifact a cs = cons Artifactâˆˆâ‚›Variant (artifact-constructor a cs)


push-down-artifact : âˆ€ {i : Size} {D : ğ”½} {A : ğ”¸} â†’ atoms A â†’ List (2ADT Variant D A) â†’ 2ADT Variant D A
push-down-artifact {A = A} a cs = go cs []
  module push-down-artifact-Implementation where
  go : âˆ€ {i : Size} {D : ğ”½} â†’ List (2ADT Variant D A) â†’ List (Variant A) â†’ 2ADT Variant D A
  go [] vs = leaf (artifact a (List.reverse vs))
  go (leaf v âˆ· cs) vs = go cs (v âˆ· vs)
  go (d âŸ¨ câ‚ , câ‚‚ âŸ© âˆ· cs) vs = d âŸ¨ go (câ‚ âˆ· cs) vs , go (câ‚‚ âˆ· cs) vs âŸ©

translate : âˆ€ {i : Size} {D : ğ”½} {A : ğ”¸}
  â†’ 2CC D i A
  â†’ 2ADT Variant D A
translate (a 2CC.-< cs >-) = push-down-artifact a (List.map translate cs)
translate (d 2CC.âŸ¨ l , r âŸ©) = d âŸ¨ translate l , translate r âŸ©

âŸ¦push-down-artifactâŸ§ : âˆ€ {i : Size} {D : ğ”½} {A : ğ”¸}
  â†’ (a : atoms A)
  â†’ (cs : List (2ADT Variant D A))
  â†’ (config : 2ADT.Configuration D)
  â†’ 2ADT.âŸ¦ push-down-artifact a cs âŸ§ config â‰¡ artifact a (List.map (Î» e â†’ 2ADT.âŸ¦ e âŸ§ config) cs)
âŸ¦push-down-artifactâŸ§ {D = D} {A = A} a cs config = go' cs []
  where
  open push-down-artifact-Implementation

  go' : âˆ€ {i : Size}
    â†’ (cs' : List (2ADT Variant D A))
    â†’ (vs : List (Variant A))
    â†’ 2ADT.âŸ¦ go a cs cs' vs âŸ§ config â‰¡ artifact a (vs Ê³++ List.map (Î» e â†’ 2ADT.âŸ¦ e âŸ§ config) cs')
  go' [] vs = Eq.sym (Eq.congâ‚‚ artifact refl (Eq.trans (List.Ê³++-defn vs) (List.++-identityÊ³ (List.reverse vs))))
  go' (leaf v âˆ· cs') vs = Eq.trans (go' cs' (v âˆ· vs)) (Eq.congâ‚‚ artifact refl (List.Ê³++-++ List.[ v ] {ys = vs}))
  go' ((d âŸ¨ câ‚ , câ‚‚ âŸ©) âˆ· cs') vs =
      2ADT.âŸ¦ d âŸ¨ go a cs (câ‚ âˆ· cs') vs , go a cs (câ‚‚ âˆ· cs') vs âŸ© âŸ§ config
    â‰¡âŸ¨âŸ©
      (if config d
        then 2ADT.âŸ¦ go a cs (câ‚ âˆ· cs') vs âŸ§ config
        else 2ADT.âŸ¦ go a cs (câ‚‚ âˆ· cs') vs âŸ§ config)
    â‰¡âŸ¨ Eq.congâ‚‚ (if config d then_else_) (go' (câ‚ âˆ· cs') vs) (go' (câ‚‚ âˆ· cs') vs) âŸ©
      (if config d
        then artifact a (vs Ê³++ List.map (Î» e â†’ 2ADT.âŸ¦ e âŸ§ config) (câ‚ âˆ· cs'))
        else artifact a (vs Ê³++ List.map (Î» e â†’ 2ADT.âŸ¦ e âŸ§ config) (câ‚‚ âˆ· cs')))
    â‰¡Ë˜âŸ¨ Bool.push-function-into-if (Î» c â†’ artifact a (vs Ê³++ List.map (Î» e â†’ 2ADT.âŸ¦ e âŸ§ config) (c âˆ· cs'))) (config d) âŸ©
      artifact a (vs Ê³++ List.map (Î» e â†’ 2ADT.âŸ¦ e âŸ§ config) ((if config d then câ‚ else câ‚‚) âˆ· cs'))
    â‰¡âŸ¨âŸ©
      artifact a (vs Ê³++ 2ADT.âŸ¦ if config d then câ‚ else câ‚‚ âŸ§ config âˆ· List.map (Î» e â†’ 2ADT.âŸ¦ e âŸ§ config) cs')
    â‰¡âŸ¨ Eq.congâ‚‚ artifact refl (Eq.congâ‚‚ _Ê³++_ {x = vs} refl (Eq.congâ‚‚ _âˆ·_ (Bool.push-function-into-if (Î» e â†’ 2ADT.âŸ¦ e âŸ§ config) (config d)) refl)) âŸ©
      artifact a (vs Ê³++ (if config d then 2ADT.âŸ¦ câ‚ âŸ§ config else 2ADT.âŸ¦ câ‚‚ âŸ§ config) âˆ· List.map (Î» e â†’ 2ADT.âŸ¦ e âŸ§ config) cs')
    â‰¡âŸ¨âŸ©
      artifact a (vs Ê³++ 2ADT.âŸ¦ d âŸ¨ câ‚ , câ‚‚ âŸ© âŸ§ config âˆ· List.map (Î» e â†’ 2ADT.âŸ¦ e âŸ§ config) cs')
    â‰¡âŸ¨âŸ©
      artifact a (vs Ê³++ List.map (Î» e â†’ 2ADT.âŸ¦ e âŸ§ config) (d âŸ¨ câ‚ , câ‚‚ âŸ© âˆ· cs'))
    âˆ

preserves-â‰— : âˆ€ {i : Size} {D : ğ”½} {A : ğ”¸}
  â†’ (expr : 2CC D i A)
  â†’ 2ADT.âŸ¦ translate expr âŸ§ â‰— 2CC.âŸ¦ expr âŸ§
preserves-â‰— {D = D} {A = A} (a 2CC.-< cs >-) config =
    2ADT.âŸ¦ translate (a 2CC.-< cs >-) âŸ§ config
  â‰¡âŸ¨âŸ©
    2ADT.âŸ¦ push-down-artifact a (List.map translate cs) âŸ§ config
  â‰¡âŸ¨ âŸ¦push-down-artifactâŸ§ a (List.map translate cs) config âŸ©
    artifact a (List.map (Î» e â†’ 2ADT.âŸ¦ e âŸ§ config) (List.map translate cs))
  â‰¡Ë˜âŸ¨ Eq.congâ‚‚ artifact refl (List.map-âˆ˜ cs) âŸ©
    artifact a (List.map (Î» e â†’ 2ADT.âŸ¦ translate e âŸ§ config) cs)
  â‰¡âŸ¨ Eq.congâ‚‚ artifact refl (List.map-cong (Î» e â†’ preserves-â‰— e config) cs) âŸ©
    artifact a (List.map (Î» e â†’ 2CC.âŸ¦ e âŸ§ config) cs)
  â‰¡âŸ¨âŸ©
    2CC.âŸ¦ a 2CC.-< cs >- âŸ§ config
  âˆ
preserves-â‰— (d 2CC.âŸ¨ l , r âŸ©) config =
    2ADT.âŸ¦ translate (d 2CC.âŸ¨ l , r âŸ©) âŸ§ config
  â‰¡âŸ¨âŸ©
    2ADT.âŸ¦ d âŸ¨ translate l , translate r âŸ© âŸ§ config
  â‰¡âŸ¨âŸ©
    (if config d then 2ADT.âŸ¦ translate l âŸ§ config else 2ADT.âŸ¦ translate r âŸ§ config)
  â‰¡Ë˜âŸ¨ Bool.push-function-into-if (Î» e â†’ 2ADT.âŸ¦ translate e âŸ§ config) (config d) âŸ©
    2ADT.âŸ¦ translate (if config d then l else r) âŸ§ config
  â‰¡âŸ¨ preserves-â‰— (if config d then l else r) config âŸ©
    2CC.âŸ¦ if config d then l else r âŸ§ config
  â‰¡âŸ¨âŸ©
    2CC.âŸ¦ d 2CC.âŸ¨ l , r âŸ© âŸ§ config
  âˆ

preserves : âˆ€ {i : Size} {D : ğ”½} {A : ğ”¸}
  â†’ (expr : 2CC D i A)
  â†’ 2ADT.âŸ¦ translate expr âŸ§ â‰…[ id ][ id ] 2CC.âŸ¦ expr âŸ§
preserves expr = â‰—â†’â‰…[] (preserves-â‰— expr)

2CCâ†’2ADT : âˆ€ {i : Size} {D : ğ”½} â†’ LanguageCompiler (2CCL {i} D) (2ADTL Variant D)
2CCâ†’2ADT .LanguageCompiler.compile = translate
2CCâ†’2ADT .LanguageCompiler.config-compiler expr .to = id
2CCâ†’2ADT .LanguageCompiler.config-compiler expr .from = id
2CCâ†’2ADT .LanguageCompiler.preserves expr = â‰…[]-sym (preserves expr)

2ADTâ‰½2CC : âˆ€ {D : ğ”½} â†’ 2ADTL Variant D â‰½ 2CCL D
2ADTâ‰½2CC = expressiveness-from-compiler 2CCâ†’2ADT
