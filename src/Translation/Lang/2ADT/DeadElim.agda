open import Framework.Definitions using (ğ”½; ğ•; ğ”¸; ğ”¼)
open import Relation.Binary using (DecidableEquality; Rel)
module Translation.Lang.2ADT.DeadElim
  (F : ğ”½)
  (V : ğ•)
  (_==_ : DecidableEquality F)
  where

open import Function using (id; _âˆ˜_)

open import Data.Bool using (Bool; true; false; if_then_else_)
open import Data.Empty using (âŠ¥-elim)
open import Data.List using (List; []; _âˆ·_)
open import Data.List.Relation.Unary.Any using (here; there)
open import Data.List.Relation.Unary.All using ([]; _âˆ·_)
open import Data.List.Relation.Unary.AllPairs using ([]; _âˆ·_)

open import Relation.Nullary.Negation using (Â¬_)
open import Relation.Nullary.Decidable using (yes; no; toWitness)
open import Relation.Binary.PropositionalEquality as Eq using (_â‰¡_; refl; sym)
open Eq.â‰¡-Reasoning

open import Framework.VariabilityLanguage
open import Framework.Compiler
open import Data.EqIndexedSet using (_â‰…[_][_]_; â‰â†’â‰…[])
open import Lang.2ADT
open import Lang.2ADT.Path F V _==_

{-
A 2ADT is undead if it does not contain any dead branches.
This is the case if any path from the root to a leaf does not contain
a feature name twice.
-}
Undead : âˆ€ {A} (e : 2ADT V F A) â†’ Set
Undead e = âˆ€ (p : Path) â†’ p starts-at e â†’ Unique p

{-
A leaf node is always undead.
-}
undead-leaf : âˆ€ {A} {v : V A}
    ---------------
  â†’ Undead (leaf v)
undead-leaf .[] tleaf = []

{-
If a choice is undead, so is its left alternative.
-}
undead-left : âˆ€ {A} {D} {l r : 2ADT V F A}
  â†’ Undead (D âŸ¨ l , r âŸ©)
    --------------------
  â†’ Undead l
undead-left {D = D} u-chc p t with u-chc (D â†£ true âˆ· p) (walk-left t)
... | _ âˆ· u-p = u-p

{-
If a choice is undead, so is its right alternative.
-}
undead-right : âˆ€ {A} {D} {l r : 2ADT V F A}
  â†’ Undead (D âŸ¨ l , r âŸ©)
    --------------------
  â†’ Undead r
undead-right {D = D} u-chc p t with u-chc (D â†£ false âˆ· p) (walk-right t)
... | _ âˆ· u-p = u-p

{-
If two expressions l and r are undead and do
not contain the feature name D,
then the choice D âŸ¨ l , r âŸ© is undead, too.
-}
undead-choice : âˆ€ {A} {D} {l r : 2ADT V F A}
  â†’ Undead l
  â†’ Undead r
    -- It might be handy to introduce a new predicate for containment of feature names in expressions D âˆˆ l later.
  â†’ D âˆ‰' l
  â†’ D âˆ‰' r
    --------------------------------------
  â†’ Undead (D âŸ¨ l , r âŸ©)
undead-choice u-l u-r Dâˆ‰l Dâˆ‰r (.(_ â†£ true ) âˆ· p) (walk-left  t) = âˆ‰â†’All-different p (Dâˆ‰l p t) âˆ· (u-l p t)
undead-choice u-l u-r Dâˆ‰l Dâˆ‰r (.(_ â†£ false) âˆ· p) (walk-right t) = âˆ‰â†’All-different p (Dâˆ‰r p t) âˆ· (u-r p t)

record Undead2ADT (A : ğ”¸) : Set where
  constructor _âŠš_ -- \oo
  field
    node   : 2ADT V F A
    undead : Undead node
open Undead2ADT public

âŸ¦_âŸ§áµ¤ : ğ”¼-Semantics V (Configuration F) Undead2ADT
âŸ¦_âŸ§áµ¤ = âŸ¦_âŸ§ âˆ˜ node

Undead2ADTL : VariabilityLanguage V
Undead2ADTL = âŸª Undead2ADT , Configuration F , âŸ¦_âŸ§áµ¤ âŸ«

{-
Kills all dead branches within a given expression,
assuming that some features were already defined.
-}
kill-dead-below : âˆ€ {A}
  â†’ (defined : Path)
  â†’ 2ADT V F A
  â†’ 2ADT V F A
kill-dead-below _ (leaf v) = leaf v
kill-dead-below defined (D âŸ¨ l , r âŸ©) with D âˆˆ? defined
--- The current choice was already encountered above this choice.
--- This means, this choice is dominated (see choice domination).
--- This in turn means, that one of the choice's alternatives is dead because it cannot
--- be selected anymore.
... | yes Dâˆˆdefined =
  if getValue Dâˆˆdefined
  then kill-dead-below defined l
  else kill-dead-below defined r
-- The current choice was not encountered before. We follow both paths recursively,
-- adding our choice information to each path.
... | no Dâˆ‰defined = D âŸ¨ kill-dead-below (D â†£ true âˆ· defined) l , kill-dead-below (D â†£ false âˆ· defined) r âŸ©

{-
If a feature name was already defined,
then any path starting at an expression,
in which dead branches were eliminated accordingly,
does not contain that feature name.
-}
kill-dead-eliminates-defined-features : âˆ€ {A}
  â†’ (defined : Path)
  â†’ (D : F)
  â†’ D âˆˆ defined
  â†’ (e : 2ADT V F A)
  â†’ D âˆ‰' kill-dead-below defined e
kill-dead-eliminates-defined-features _ _ _ (leaf _) .[] tleaf ()
kill-dead-eliminates-defined-features defined _ _ (D' âŸ¨ _ , _ âŸ©) _ _ _ with D' âˆˆ? defined
kill-dead-eliminates-defined-features defined D D-def (D' âŸ¨ l , r âŸ©) p t Dâˆˆp | yes D'-def with getValue D'-def
... | true  = kill-dead-eliminates-defined-features defined D D-def l p t Dâˆˆp
... | false = kill-dead-eliminates-defined-features defined D D-def r p t Dâˆˆp
kill-dead-eliminates-defined-features _ D _ (D' âŸ¨ _ , _ âŸ©) _ _ _
  | no Â¬D'-def with D == D'
kill-dead-eliminates-defined-features _ _ D-def (_  âŸ¨ _ , _ âŸ©) _ _ _
  | no Â¬D'-def | yes refl = âŠ¥-elim (Â¬D'-def D-def)
kill-dead-eliminates-defined-features _ _       _     (D' âŸ¨ _ , _ âŸ©) ((.D' â†£ true) âˆ· _) (walk-left _) (here D=D')
  | no _       | no Dâ‰ D'  = âŠ¥-elim (Dâ‰ D' (toWitness D=D'))
kill-dead-eliminates-defined-features defined D D-def (D' âŸ¨ l , _ âŸ©) ((.D' â†£ true) âˆ· p) (walk-left t) (there Dâˆˆp)
  | no Â¬D'-def | no Dâ‰ D'  = kill-dead-eliminates-defined-features (D' â†£ true âˆ· defined) D (there D-def) l p t Dâˆˆp
kill-dead-eliminates-defined-features _ _       _     (D' âŸ¨ _ , _ âŸ©) ((.D' â†£ false) âˆ· _) (walk-right _) (here D=D')
  | no _       | no Dâ‰ D'  = âŠ¥-elim (Dâ‰ D' (toWitness D=D'))
kill-dead-eliminates-defined-features defined D D-def (D' âŸ¨ _ , r âŸ©) ((.D' â†£ false) âˆ· p) (walk-right t) (there Dâˆˆp)
  | no Â¬D'-def | no Dâ‰ D'  = kill-dead-eliminates-defined-features (D' â†£ false âˆ· defined) D (there D-def) r p t Dâˆˆp

{-
Proof that kill-dead is correct,
which means that the resulting tree
is undead.
-}
kill-dead-correct : âˆ€ {A}
  â†’ (defined : Path)
  â†’ (e : 2ADT V F A)
  â†’ Undead (kill-dead-below defined e)
kill-dead-correct _ (leaf v) = undead-leaf
kill-dead-correct defined (D âŸ¨ _ , _ âŸ©) with D âˆˆ? defined
kill-dead-correct defined (_ âŸ¨ l , r âŸ©) | yes Dâˆˆdefined with getValue Dâˆˆdefined
... | true  = kill-dead-correct defined l
... | false = kill-dead-correct defined r
kill-dead-correct defined (D âŸ¨ l , r âŸ©) | no  Dâˆ‰defined =
  undead-choice
  (kill-dead-correct (D â†£ true  âˆ· defined) l)
  (kill-dead-correct (D â†£ false âˆ· defined) r)
  (kill-dead-eliminates-defined-features (D â†£ true  âˆ· defined) D (here (is-refl D true )) l)
  (kill-dead-eliminates-defined-features (D â†£ false âˆ· defined) D (here (is-refl D false)) r)

{-
Dead branch elimination of 2ADTs.
-}
kill-dead : âˆ€ {A}
  â†’ 2ADT V F A
  â†’ Undead2ADT A
kill-dead e = kill-dead-below [] e âŠš kill-dead-correct [] e

kill-dead-preserves-below-partial-configs : âˆ€ {A : ğ”¸}
  â†’ (e : 2ADT V F A)
  â†’ (defined : Path)
  â†’ (c : Configuration F)
  â†’ defined âŠ‘ c
  â†’ âŸ¦ e âŸ§ c â‰¡ âŸ¦ kill-dead-below defined e âŸ§ c
kill-dead-preserves-below-partial-configs (leaf _) _ _ _ = refl
kill-dead-preserves-below-partial-configs (D âŸ¨ l , r âŸ©) def c defâŠ‘c with D âˆˆ? def
kill-dead-preserves-below-partial-configs (D âŸ¨ l , r âŸ©) def c defâŠ‘c | yes Dâˆˆdef
  rewrite lookup-partial defâŠ‘c Dâˆˆdef
  with c D
... | true  = kill-dead-preserves-below-partial-configs l def c defâŠ‘c
... | false = kill-dead-preserves-below-partial-configs r def c defâŠ‘c
kill-dead-preserves-below-partial-configs (D âŸ¨ l , r âŸ©) def c defâŠ‘c | no Dâˆ‰def
  with c D in eq
... | true  = kill-dead-preserves-below-partial-configs l ((D â†£  true) âˆ· def) c (eq âˆ· defâŠ‘c)
... | false = kill-dead-preserves-below-partial-configs r ((D â†£ false) âˆ· def) c (eq âˆ· defâŠ‘c)

kill-dead-preserves : âˆ€ {A : ğ”¸}
  â†’ (e : 2ADT V F A)
  â†’ âŸ¦ e âŸ§ â‰…[ id ][ id ] âŸ¦ kill-dead e âŸ§áµ¤
kill-dead-preserves e = â‰â†’â‰…[] (Î» c â†’ kill-dead-preserves-below-partial-configs e [] c [])

kill-dead-compiler : LanguageCompiler (2ADTL V F) Undead2ADTL
kill-dead-compiler = record
  { compile = kill-dead
  ; config-compiler = Î» _ â†’ record { to = id ; from = id }
  ; preserves = kill-dead-preserves
  }
