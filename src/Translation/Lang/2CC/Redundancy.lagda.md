This module defines an intra-language compiler for binary
choice calculus that eliminates any redundant choices.
If a choice is nested somewhere below a choice with the same
dimensions, the inner choice effectively, cannot be configured anymore.
This phenomenon is known as "choice domination" in the choice calculus
papers.
Example:

> D âŸ¨ l , D âŸ¨ x , y âŸ© âŸ©

Here, `x` is dead because in order to reach it, we would have to configure
the dimension `D` to be `false` (to go right in the outer choice) and then
to `true` (to go left in the inner choice) which is not legal.
We can hence simplify this choice to:

> D âŸ¨ l , y âŸ©

which is exactly what happens in this module.

TODO: The compiler currently is forgetful. While it produces an expression
      without dead choices, it does not generate a proof (or typing) for the
      resulting expression that it indeed has this property.
-}
```agda
open import Framework.Definitions using (ğ”¸; ğ”½)
open import Relation.Binary.Definitions using (DecidableEquality)
module Translation.Lang.2CC.Redundancy (Dimension : ğ”½) (_==_ : DecidableEquality Dimension) where

open import Function using (id)
open import Size using (Size; âˆ)
open import Data.Bool using (Bool; true; false)
open import Data.Empty using (âŠ¥-elim)
open import Data.List as List using (List; map)
import Data.List.Properties as List
open import Data.Maybe using (Maybe; just; nothing)

open import Relation.Nullary using (yes; no)
open import Relation.Binary.PropositionalEquality as Eq using (refl; _â‰¡_)
open Eq.â‰¡-Reasoning

open import Framework.Compiler using (LanguageCompiler)
open import Framework.Variants as V using (Rose)
open import Util.AuxProofs using (trueâ‰¢false)

open import Data.EqIndexedSet using (â‰—â†’â‰…[]; â‰…[]-sym)

open import Lang.2CC
```

A scope remembers any dominating outer choices.
When traversing a `2CC` expression, the scope keeps track
of any visited choices above the current expression and remembers
whether the outer choice must be configured to true or false in
order to reach this expression.
To type theory / PL theory people, a scope may appear as an environment Î“.
```agda
Scope : Set
Scope = Dimension â†’ Maybe Bool

{-|
Refines a scope by adding a new value for a given dimension.
-}
refine : Scope â†’ Dimension â†’ Bool â†’ Scope
refine scope D b D' with D == D'
refine scope D b D' | yes _ = just b
refine scope D b D' | no _ = scope D'

{-|
Recursively eliminates dominated choices assuming the given outer scope.
-}
eliminate-redundancy-in : âˆ€ {i : Size} {A : ğ”¸} â†’ Scope â†’ 2CC Dimension i A â†’ 2CC Dimension âˆ A
eliminate-redundancy-in scope (a -< es >-) = a -< map (eliminate-redundancy-in scope) es >-
eliminate-redundancy-in scope (D âŸ¨ l , r âŸ©) with scope D
... | just true  = eliminate-redundancy-in scope l
... | just false = eliminate-redundancy-in scope r
... | nothing    = D âŸ¨ eliminate-redundancy-in (refine scope D true ) l
                    , eliminate-redundancy-in (refine scope D false) r
                    âŸ©

{-|
Recursively eliminates dominated choices.
-}
eliminate-redundancy : âˆ€ {i : Size} {A : ğ”¸} â†’ 2CC Dimension i A â†’ 2CC Dimension âˆ A
eliminate-redundancy = eliminate-redundancy-in (Î» _ â†’ nothing)

{-|
Proof that eliminating dominated choices does not change semantics.
-}
preserves-â‰— : âˆ€ {i : Size} {A : ğ”¸}
  â†’ (scope : Scope)
  â†’ (c : Configuration Dimension)
  â†’ (âˆ€ {D : Dimension} {b : Bool} â†’ scope D â‰¡ just b â†’ c D â‰¡ b)
  â†’ (e : 2CC Dimension i A)
  â†’ âŸ¦ eliminate-redundancy-in scope e âŸ§ c â‰¡ âŸ¦ e âŸ§ c
preserves-â‰— scope c p (a -< cs >-) =
    âŸ¦ eliminate-redundancy-in scope (a -< cs >-) âŸ§ c
  â‰¡âŸ¨âŸ©
    âŸ¦ a -< map (eliminate-redundancy-in scope) cs >- âŸ§ c
  â‰¡âŸ¨âŸ©
    a V.-< map (Î» e â†’ âŸ¦ e âŸ§ c) (map (eliminate-redundancy-in scope) cs) >-
  â‰¡âŸ¨ Eq.cong (a V.-<_>-) (List.map-âˆ˜ cs) âŸ¨
    a V.-< map (Î» e â†’ âŸ¦ eliminate-redundancy-in scope e âŸ§ c) cs >-
  â‰¡âŸ¨ Eq.cong (a V.-<_>-) (List.map-cong (Î» e â†’ preserves-â‰— scope c p e) cs) âŸ©
    a V.-< map (Î» e â†’ âŸ¦ e âŸ§ c) cs >-
  â‰¡âŸ¨âŸ©
    âŸ¦ a -< cs >- âŸ§ c
  âˆ
preserves-â‰— scope c p (D âŸ¨ l , r âŸ©) with scope D in scope-D
preserves-â‰— scope c p (D âŸ¨ l , r âŸ©) | just true with c D in c-D
preserves-â‰— scope c p (D âŸ¨ l , r âŸ©) | just true | false = âŠ¥-elim (trueâ‰¢false (p scope-D) c-D)
preserves-â‰— scope c p (D âŸ¨ l , r âŸ©) | just true | true = preserves-â‰— scope c p l
preserves-â‰— scope c p (D âŸ¨ l , r âŸ©) | just false with c D in c-D
preserves-â‰— scope c p (D âŸ¨ l , r âŸ©) | just false | false = preserves-â‰— scope c p r
preserves-â‰— scope c p (D âŸ¨ l , r âŸ©) | just false | true = âŠ¥-elim (trueâ‰¢false c-D (p scope-D))
preserves-â‰— scope c p (D âŸ¨ l , r âŸ©) | nothing with c D in c-D
preserves-â‰— scope c p (D âŸ¨ l , r âŸ©) | nothing | true = preserves-â‰— (refine scope D true) c lemma l
  where
  lemma : âˆ€ {D' : Dimension} {b : Bool} â†’ refine scope D true D' â‰¡ just b â†’ c D' â‰¡ b
  lemma {D' = D'} p' with D == D'
  lemma {b = true} p' | yes refl = c-D
  lemma p' | no Dâ‰¢D' = p p'
preserves-â‰— scope c p (D âŸ¨ l , r âŸ©) | nothing | false = preserves-â‰— (refine scope D false) c lemma r
  where
  lemma : âˆ€ {D' : Dimension} {b : Bool} â†’ refine scope D false D' â‰¡ just b â†’ c D' â‰¡ b
  lemma {D' = D'} p' with D == D'
  lemma {b = false} p' | yes refl = c-D
  lemma p' | no Dâ‰¢D' = p p'

{-|
An intra-language compiler for eliminating redundant choices.
-}
Redundancy-Elimination : LanguageCompiler (2CCL Dimension) (2CCL Dimension)
Redundancy-Elimination = record
  { compile = eliminate-redundancy
  ; config-compiler = Î» _ â†’ record { to = id ; from = id }
  ; preserves = Î» e â†’ â‰…[]-sym (â‰—â†’â‰…[] Î» c â†’ preserves-â‰— (Î» _ â†’ nothing) c (Î» where ()) e)
  }
```
