# Theorems to Prove Completeness

```agda
open import Framework.Definitions using (ğ•)
module Framework.Proof.Transitive (V : ğ•) where

open import Data.Product using (_,_; _Ã—_; âˆ„-syntax)
open import Framework.VariabilityLanguage using (VariabilityLanguage)
open import Framework.Properties.Completeness V
open import Framework.Properties.Soundness V
open import Framework.Relation.Expressiveness V
open import Data.EqIndexedSet
```

## Conclusions

If a language `L` is complete, and another language `M` is as expressive as `L`, then also `M` is complete.
The intuition is that `M` can express everything `L` can express which in turn is "everything" by completeness.
Thus also `M` is complete.

**Proof Sketch:**
Let V be an arbitrary set of variants.
Since L is complete, there exists an expression l âˆˆ L that describes V.
By assumption, M is as expressive as L.
Thus, there exists an expression m âˆˆ M that also describes V.
Since V was picked arbitrarily, M can encode any set of variants.
Thus, M is complete.
```agda
completeness-by-expressiveness : âˆ€ {L M : VariabilityLanguage V}
  â†’ Complete M
  â†’ L â‰½ M
    -----------------------------------
  â†’ Complete L
completeness-by-expressiveness encode-in-M M-to-L vs with encode-in-M vs
... | m , vsâ‰…m with M-to-L m
...   | l , mâ‰…l = l , â‰…-trans vsâ‰…m mâ‰…l

incompleteness-by-expressiveness : âˆ€ {L M : VariabilityLanguage V}
  â†’ Incomplete L
  â†’ L â‰½ M
    -----------------------------------
  â†’ Incomplete M
incompleteness-by-expressiveness L-incomp Lâ‰½M M-comp = L-incomp (completeness-by-expressiveness M-comp Lâ‰½M)
```

If a language `L` is sound and at least as expressive as another language `M`, then also `M` is sound.
The intuition is that `L` can express everything `M` and everything expressed by `L` is valid.
So also everything expressed in `M` must be valid.
```agda
soundness-by-expressiveness : âˆ€ {L M : VariabilityLanguage V}
  â†’ Sound L
  â†’ L â‰½ M
    --------
  â†’ Sound M
soundness-by-expressiveness L-sound M-to-L m with M-to-L m
... | l , mâ‰…l with L-sound l
...   | n , v , vâ‰…l = n , v , â‰…-trans vâ‰…l (â‰…-sym mâ‰…l)

unsoundness-by-expressiveness : âˆ€ {L M : VariabilityLanguage V}
  â†’ Unsound M
  â†’ L â‰½ M
    -----------------------------------
  â†’ Unsound L
unsoundness-by-expressiveness M-unsound Lâ‰½M M-sound = M-unsound (soundness-by-expressiveness M-sound Lâ‰½M)
```

Conversely, we can conclude that any complete language is at least as expressive as any other variability language.

**Proof sketch:**
Given an arbitrary expression eË¢ of our target language LË¢, we have to show that there exists an expression eâ‚Š in our complete language Lá¶œ that is variant-equivalent to eË¢.
By soundness of LË¢ we can compute the variant map of eË¢.
By completeness of Lá¶œ, we can encode any variant map as an expression eá¶œ âˆˆ Lá¶œ.
```agda
expressiveness-by-completeness-and-soundness : âˆ€ {Lá¶œ LË¢ : VariabilityLanguage V}
  â†’ Complete Lá¶œ
  â†’ Sound LË¢
    ----------------------------------
  â†’ Lá¶œ â‰½ LË¢
expressiveness-by-completeness-and-soundness comp sound eË¢ with sound eË¢
... | p , v , vâ‰…âŸ¦eË¢âŸ§ with comp v
...   | eá¶œ , vâ‰…âŸ¦eá¶œâŸ§ = eá¶œ , â‰…-trans (â‰…-sym vâ‰…âŸ¦eË¢âŸ§) vâ‰…âŸ¦eá¶œâŸ§
```

If a language `Lâ‚Š` is complete and another language `Lâ‚‹` is incomplete then `Lâ‚‹` less expressive than `Lâ‚Š`.

**Proof sketch:**
Assuming `Lâ‚‹` is as expressive as `Lâ‚Š`, and knowing that `Lâ‚Š` is complete, we can conclude that also `Lâ‚‹` is complete (via `completeness-by-exoressiveness` above).
Yet, we already know that Lâ‚‹ is incomplete.
This yields a contradiction.
```agda
less-expressive-from-completeness : âˆ€ {Lâ‚Š Lâ‚‹ : VariabilityLanguage V}
  â†’   Complete Lâ‚Š
  â†’ Incomplete Lâ‚‹
    ------------------------------
  â†’ Lâ‚‹ â‹¡ Lâ‚Š
less-expressive-from-completeness Lâ‚Š-comp Lâ‚‹-incomp Lâ‚‹-as-expressive-as-Lâ‚Š =
    Lâ‚‹-incomp (completeness-by-expressiveness Lâ‚Š-comp Lâ‚‹-as-expressive-as-Lâ‚Š)
```

```agda
less-expressive-from-soundness : âˆ€ {Lâ‚Š Lâ‚‹ : VariabilityLanguage V}
  â†’   Sound Lâ‚Š
  â†’ Unsound Lâ‚‹
    ------------------------------
  â†’ Lâ‚Š â‹¡ Lâ‚‹
less-expressive-from-soundness Lâ‚Š-sound Lâ‚‹-unsound Lâ‚‹â‰½Lâ‚Š =
    Lâ‚‹-unsound (soundness-by-expressiveness Lâ‚Š-sound Lâ‚‹â‰½Lâ‚Š)
```

Combined with `expressiveness-by-completeness` we can even further conclude that Lâ‚Š is more expressive than Lâ‚‹:
```agda
more-expressive-by-completeness : âˆ€ {Lâ‚Š Lâ‚‹ : VariabilityLanguage V}
  â†’ Complete Lâ‚Š
  â†’ Sound Lâ‚‹
  â†’ Incomplete Lâ‚‹
    -------------
  â†’ Lâ‚Š â‰» Lâ‚‹
more-expressive-by-completeness Lâ‚Š-comp Lâ‚‹-sound Lâ‚‹-incomp =
    expressiveness-by-completeness-and-soundness Lâ‚Š-comp Lâ‚‹-sound
  , less-expressive-from-completeness Lâ‚Š-comp Lâ‚‹-incomp

more-expressive-by-soundness : âˆ€ {Lâ‚Š Lâ‚‹ : VariabilityLanguage V}
  â†’ Sound Lâ‚Š
  â†’ Complete Lâ‚‹
  â†’ Unsound Lâ‚‹
    -----------
  â†’ Lâ‚‹ â‰» Lâ‚Š
more-expressive-by-soundness Lâ‚Š-sound Lâ‚‹-comp Lâ‚‹-unsound =
    expressiveness-by-completeness-and-soundness Lâ‚‹-comp Lâ‚Š-sound
  , less-expressive-from-soundness Lâ‚Š-sound Lâ‚‹-unsound
```

```agda
complete-is-most-expressive : âˆ€ {L : VariabilityLanguage V}
  â†’ Complete L
    ----------------
  â†’ âˆ„[ M ] (Sound M Ã— M â‰» L)
complete-is-most-expressive L-comp (M , M-sound , Mâ‰½L , Lâ‹¡M) =
  Lâ‹¡M (expressiveness-by-completeness-and-soundness L-comp M-sound)
```
